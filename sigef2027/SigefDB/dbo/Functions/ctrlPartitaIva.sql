CREATE FUNCTION [dbo].[ctrlPartitaIva]
(
	@PIVA VARCHAR(30)
)
RETURNS BIT
AS BEGIN
	--DECLARE @PIVA VARCHAR(30);SET @PIVA='00325720423'

	-- P.IVA ESTERA
	IF LEN(@PIVA)<9 RETURN 0
	DECLARE @PREFISSO_PAESE CHAR(3)
	SELECT @PREFISSO_PAESE=SUBSTRING(@PIVA,1,3)
	IF @PREFISSO_PAESE IN ('DE ','GB ','FR ','BE ','AT ') RETURN 1
	
	-- P.IVA ITALIANA
	IF LEN(@PIVA)<>11 RETURN 0
	DECLARE @COUNTER INT,@CIFRA_CORRENTE CHAR(1),@VALORE_CIFRA INT,@SOMMA1 DECIMAL(15,2),@CHECK INT
	SET @COUNTER=1
	WHILE @COUNTER<11 BEGIN	
		SET @CIFRA_CORRENTE=SUBSTRING(@PIVA,@COUNTER,50)
		IF ASCII(@CIFRA_CORRENTE)<48 OR ASCII(@CIFRA_CORRENTE)>57 RETURN 0
		SET @VALORE_CIFRA=CAST(@CIFRA_CORRENTE AS INT)		
		IF @COUNTER%2=0 BEGIN
			-- CIFRE PARI (MOLTIPLICO *2)
			SET @VALORE_CIFRA=2*@VALORE_CIFRA
			IF @VALORE_CIFRA>9 SET @VALORE_CIFRA=@VALORE_CIFRA-9		
		END
		SET @SOMMA1=ISNULL(@SOMMA1,0)+@VALORE_CIFRA
		SET @COUNTER=@COUNTER+1
	END
	SET @CHECK=@SOMMA1%10
	IF @CHECK>0 SET @CHECK=10-@CHECK
	IF @CHECK<>CAST(SUBSTRING(@PIVA,11,1) AS INT) RETURN 0
	RETURN 1
END
