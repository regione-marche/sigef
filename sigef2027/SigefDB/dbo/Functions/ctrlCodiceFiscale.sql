CREATE FUNCTION [dbo].[ctrlCodiceFiscale]
(
	@CF VARCHAR(30)
)
RETURNS BIT
AS BEGIN	
	--DECLARE @CF VARCHAR(30);SET @CF=''

	IF LEN(@CF)<>16 RETURN 0
	DECLARE @SET1 CHAR(36),@SET2 CHAR(36),@SET_PARI CHAR(26),@SET_DISPARI CHAR(26),@LETTERA_CORRENTE CHAR(1),@COD_ASCII_CORRENTE INT,
		@COUNTER INT,@CHECK INT
	SET @SET1='0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'
	SET @SET2='ABCDEFGHIJABCDEFGHIJKLMNOPQRSTUVWXYZ'
	SET @SET_PARI='ABCDEFGHIJKLMNOPQRSTUVWXYZ'
	SET @SET_DISPARI='BAKPLCQDREVOSFTGUHMINJWZYX'
	SET @CF=UPPER(@CF)
	SET @COUNTER=1
	SET @CHECK=0
	WHILE @COUNTER<16 BEGIN
		SET @LETTERA_CORRENTE=SUBSTRING(@CF,@COUNTER,1)	
			
		--CONTROLLO CORRETTEZZA CIFRE (48<>57 NUMERI, 65<>90 LETTERE MAIUSCOLE)
		SET @COD_ASCII_CORRENTE=ASCII(@LETTERA_CORRENTE)		
		IF @COUNTER IN (7,8,10,11,13,14,15) BEGIN
		-- NUMERI
			IF @COD_ASCII_CORRENTE<48 OR @COD_ASCII_CORRENTE>57 RETURN 0
		END 
		-- LETTERE
		ELSE IF @COD_ASCII_CORRENTE<65 OR @COD_ASCII_CORRENTE>90 RETURN 0
		
		IF @COUNTER%2=1 SET @CHECK=@CHECK+CHARINDEX(SUBSTRING(@SET2,CHARINDEX(@LETTERA_CORRENTE,@SET1,0),1),@SET_DISPARI,0)-1
		ELSE SET @CHECK=@CHECK+CHARINDEX(SUBSTRING(@SET2,CHARINDEX(@LETTERA_CORRENTE,@SET1,0),1),@SET_PARI,0)-1
		SET @COUNTER=@COUNTER+1
	END
	IF @CHECK%26!=ASCII(SUBSTRING(@CF,16,1))-ASCII('A') RETURN 0	
	RETURN 1
END
