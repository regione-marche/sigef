CREATE PROCEDURE [dbo].[SpPsrGetParticelleXInvestimento]
(
	@ID_PROGETTO INT,
	@COD_BELFIORE CHAR(4)=NULL,
	@FOGLIO_CATASTALE VARCHAR(4)=NULL,
	@PARTICELLA VARCHAR(5)=NULL,
	@ID_INVESTIMENTO INT=NULL,
	@CERCA BIT=0
)
AS	

	--DECLARE @ID_PROGETTO INT=946,
	--@COD_BELFIORE CHAR(4)=NULL,
	--@FOGLIO_CATASTALE VARCHAR(4)=NULL,
	--@PARTICELLA VARCHAR(5)=NULL,
	--@ID_INVESTIMENTO INT=3132,
	--@CERCA BIT=1
	
	DECLARE @DATA_RILASCIO_DOMANDA DATETIME,@ID_IMPRESA INT,@ID_FASCICOLO INT
	-- RECUPERO LA DATA DI MODIFICA/RILASCIO DEL PROGETTO
	SELECT TOP 1 @DATA_RILASCIO_DOMANDA=S.DATA,@ID_IMPRESA=P.ID_IMPRESA,@ID_FASCICOLO=P.ID_FASCICOLO
	FROM PROGETTO P INNER JOIN PROGETTO_STORICO S ON P.ID_PROGETTO=S.ID_PROGETTO AND COD_STATO IN ('P','L')	
    WHERE P.ID_PROGETTO=@ID_PROGETTO ORDER BY S.DATA DESC
    
	DECLARE @PP TABLE (ID_CATASTO INT,ID_LOCALIZZAZIONE INT,ID_IMPRESA INT,ORDINE INT)
	-- INSERISCO PRIMA LE PARTICELLE GIA' ASSEGNATE ALL'INVESTIMENTO
	INSERT INTO @PP
	SELECT L.ID_CATASTO,L.ID_LOCALIZZAZIONE,F.ID_IMPRESA,2
	FROM LOCALIZZAZIONE_INVESTIMENTO L INNER JOIN TERRITORIO T ON L.ID_CATASTO=T.ID_CATASTO
	INNER JOIN FASCICOLO_AZIENDALE F ON T.ID_FASCICOLO=F.ID_FASCICOLO
	WHERE L.ID_INVESTIMENTO=@ID_INVESTIMENTO AND F.ID_FASCICOLO=@ID_FASCICOLO
	UNION ALL
	SELECT L.ID_CATASTO,L.ID_LOCALIZZAZIONE,S.ID_SOCIO,1
	FROM LOCALIZZAZIONE_INVESTIMENTO L INNER JOIN TERRITORIO T ON L.ID_CATASTO=T.ID_CATASTO	
	INNER JOIN (
		SELECT ID_IMPRESA,MAX(ID_FASCICOLO) ID_MAX_FASCICOLO FROM FASCICOLO_AZIENDALE
		WHERE  DATA_STORICIZZAZIONE IS NOT NULL AND DATA_STORICIZZAZIONE<=@DATA_RILASCIO_DOMANDA AND 
			DATEADD(DD,365,DATA_VALIDAZIONE_FASCICOLO)>=@DATA_RILASCIO_DOMANDA GROUP BY ID_IMPRESA
		) Q2 ON T.ID_FASCICOLO=Q2.ID_MAX_FASCICOLO
	INNER JOIN IMPRESA_SOCI S ON Q2.ID_IMPRESA=S.ID_SOCIO AND 
		@DATA_RILASCIO_DOMANDA BETWEEN S.DATA_INIZIO_VALIDITA AND ISNULL(S.DATA_FINE_VALIDITA,GETDATE())
	WHERE L.ID_INVESTIMENTO=@ID_INVESTIMENTO AND S.ID_IMPRESA=@ID_IMPRESA

	IF @CERCA=1
		INSERT INTO @PP
		SELECT TOP 25 C.ID_CATASTO,L.ID_LOCALIZZAZIONE,F.ID_IMPRESA,0				
		FROM FASCICOLO_AZIENDALE F INNER JOIN TERRITORIO T ON F.ID_FASCICOLO=T.ID_FASCICOLO
		INNER JOIN CATASTO_TERRENI C ON T.ID_CATASTO=C.ID_CATASTO
		INNER JOIN COMUNI M ON C.ID_COMUNE=M.ID_COMUNE
		LEFT JOIN LOCALIZZAZIONE_INVESTIMENTO L ON C.ID_CATASTO=L.ID_CATASTO AND L.ID_INVESTIMENTO=@ID_INVESTIMENTO		
		WHERE T.ID_FASCICOLO=@ID_FASCICOLO AND M.COD_BELFIORE=@COD_BELFIORE AND L.ID_LOCALIZZAZIONE IS NULL
			AND (@FOGLIO_CATASTALE IS NULL OR C.FOGLIO_CATASTALE=@FOGLIO_CATASTALE) 
			AND (@PARTICELLA IS NULL OR C.PARTICELLA LIKE '%'+@PARTICELLA)		
		/* Unisco la query di ricerca sui fascicoli dei soci */
		UNION ALL 
        SELECT TOP 25 C.ID_CATASTO,L.ID_LOCALIZZAZIONE,S.ID_SOCIO,CASE WHEN L.ID_LOCALIZZAZIONE IS NOT NULL THEN 1 ELSE -1 END				
        FROM IMPRESA_SOCI S
		INNER JOIN (
			SELECT ID_IMPRESA,MAX(ID_FASCICOLO) ID_MAX_FASCICOLO FROM FASCICOLO_AZIENDALE
			WHERE  DATA_STORICIZZAZIONE IS NOT NULL AND DATA_STORICIZZAZIONE<=@DATA_RILASCIO_DOMANDA AND 
				DATEADD(DD,365,DATA_VALIDAZIONE_FASCICOLO)>=@DATA_RILASCIO_DOMANDA GROUP BY ID_IMPRESA
			) Q2 ON Q2.ID_IMPRESA=S.ID_SOCIO
		INNER JOIN TERRITORIO T ON T.ID_FASCICOLO = ID_MAX_FASCICOLO
		INNER JOIN CATASTO_TERRENI C ON C.ID_CATASTO = T.ID_CATASTO
		INNER JOIN COMUNI M ON C.ID_COMUNE=M.ID_COMUNE
		LEFT JOIN LOCALIZZAZIONE_INVESTIMENTO L ON L.ID_CATASTO=C.ID_CATASTO AND L.ID_INVESTIMENTO = @ID_INVESTIMENTO
        WHERE S.ID_IMPRESA=@ID_IMPRESA AND S.ATTIVO=1 AND M.COD_BELFIORE=@COD_BELFIORE AND L.ID_LOCALIZZAZIONE IS NULL
             AND (@FOGLIO_CATASTALE IS NULL OR C.FOGLIO_CATASTALE=@FOGLIO_CATASTALE) 
             AND (@PARTICELLA IS NULL OR C.PARTICELLA LIKE '%'+@PARTICELLA)
	
	SELECT TOP 25 C.*,Q1.ID_LOCALIZZAZIONE,Q1.ID_IMPRESA--,Q1.ORDINE
	FROM vCATASTO C INNER JOIN /*(
		SELECT DISTINCT ID_CATASTO,ID_LOCALIZZAZIONE,ORDINE,ID_IMPRESA FROM*/ @PP/*)*/ Q1 ON C.ID_CATASTO=Q1.ID_CATASTO
	ORDER BY ORDINE DESC,DENOMINAZIONE,FOGLIO_CATASTALE,PARTICELLA
