

CREATE PROCEDURE [dbo].[SpCalcoloPunteggioDomandaDiAiuto]
(
	@ID_PROGETTO INT, -- ID_PROGETTO_INTEGRATO
	@ID_VARIANTE INT=NULL,
	@PUNTEGGI_MANUALI VARCHAR(500)=NULL,
	@ISTRUTTORIA BIT=1,
	@OPERATORE CHAR(16)
)
AS
	--DECLARE @ID_PROGETTO INT,@ID_VARIANTE INT,@ISTRUTTORIA BIT,@OPERATORE CHAR(16),@PUNTEGGI_MANUALI VARCHAR(100); 
	--SET @ID_PROGETTO=500;SET @ID_VARIANTE=174;SET @ISTRUTTORIA=1;SET @PUNTEGGI_MANUALI='11:4.550,050|13:6.556,00|'
	
	DECLARE @ID_PRIORITA INT,@MANUALE BIT,@EVAL NVARCHAR(3000),@PUNTEGGIO_OLD DECIMAL(10,6),@PUNTEGGIO_NEW DECIMAL(10,6),
		@PESO DECIMAL(10,2),@COUNTER INT,@INDICE_ID_PRIORITA INT,@INDICE_DUEPUNTI_SUCCESSIVI INT,@INDICE_PIPE_SUCCESSIVA INT
	DECLARE @PUNTEGGIO_TEMP TABLE (ID INT IDENTITY(1,1) NOT NULL,PUNTEGGIO_TEMPORANEO DECIMAL(10,6))
	
	-- CALCOLO PRIORITA IN CHECKLIST AMMISSIBILITA E GRADUATORIA
	IF @ID_VARIANTE IS NULL BEGIN
		DECLARE CPRIORITA CURSOR FOR 
		SELECT R.ID_PRIORITA,R.FLAG_MANUALE,R.EVAL,G.PUNTEGGIO,S.PESO FROM BANDO B INNER JOIN PROGETTO P ON B.ID_BANDO=P.ID_BANDO
		INNER JOIN SCHEDA_X_PRIORITA S ON B.ID_SCHEDA_VALUTAZIONE=S.ID_SCHEDA_VALUTAZIONE
		INNER JOIN PRIORITA R ON S.ID_PRIORITA=R.ID_PRIORITA
		LEFT JOIN GRADUATORIA_PROGETTI_DETTAGLIO G ON S.ID_PRIORITA=G.ID_PRIORITA AND P.ID_PROGETTO=G.ID_PROGETTO
		WHERE P.ID_PROGETTO=@ID_PROGETTO AND S.PESO>0 ORDER BY ORDINE
		
		SET @COUNTER=0
		OPEN CPRIORITA
		FETCH NEXT FROM CPRIORITA INTO @ID_PRIORITA,@MANUALE,@EVAL,@PUNTEGGIO_OLD,@PESO
		WHILE @@FETCH_STATUS=0 BEGIN
			SET @PUNTEGGIO_NEW=0
			SET @COUNTER=@COUNTER+1	
			IF @MANUALE=1 BEGIN
				-- INIZIALIZZO
				SET @INDICE_ID_PRIORITA=CHARINDEX(CAST(@ID_PRIORITA AS VARCHAR(18))+':',@PUNTEGGI_MANUALI)
				IF @INDICE_ID_PRIORITA>0 BEGIN				
					SET @INDICE_DUEPUNTI_SUCCESSIVI=CHARINDEX(':',@PUNTEGGI_MANUALI,@INDICE_ID_PRIORITA)
					SET @INDICE_PIPE_SUCCESSIVA=CHARINDEX('|',@PUNTEGGI_MANUALI,@INDICE_ID_PRIORITA)
					IF @INDICE_DUEPUNTI_SUCCESSIVI>0 AND @INDICE_PIPE_SUCCESSIVA>0 BEGIN
						BEGIN TRY
							SET @PUNTEGGIO_NEW=CAST(REPLACE(REPLACE(SUBSTRING(@PUNTEGGI_MANUALI,@INDICE_DUEPUNTI_SUCCESSIVI+1,
								@INDICE_PIPE_SUCCESSIVA-@INDICE_DUEPUNTI_SUCCESSIVI-1),'.',''),',','.') AS DECIMAL(10,6))
						END TRY
						BEGIN CATCH
							SET @PUNTEGGIO_NEW=0
						END CATCH
					END
				END
			END
			ELSE IF @EVAL IS NOT NULL BEGIN
				BEGIN TRY				
					INSERT @PUNTEGGIO_TEMP
					EXEC SpExecPrioritaQuerySql @EVAL,@ID_PROGETTO,@ID_VARIANTE,@ISTRUTTORIA
					SELECT TOP 1 @PUNTEGGIO_NEW=PUNTEGGIO_TEMPORANEO FROM @PUNTEGGIO_TEMP ORDER BY ID DESC
					DELETE FROM @PUNTEGGIO_TEMP
				END TRY
				BEGIN CATCH
					SET @PUNTEGGIO_NEW=0
				END CATCH
				-- SOLO LE PRIORITA CALCOLATE VENGONO PESATE
				SET @PUNTEGGIO_NEW=ISNULL(@PUNTEGGIO_NEW*@PESO,0)
			END
			
			IF @PUNTEGGIO_OLD IS NULL
				INSERT INTO GRADUATORIA_PROGETTI_DETTAGLIO (ID_PRIORITA,ID_PROGETTO,PUNTEGGIO,DATA_VALUTAZIONE,OPERATORE,MODIFICA_MANUALE)
				VALUES(@ID_PRIORITA,@ID_PROGETTO,@PUNTEGGIO_NEW,GETDATE(),@OPERATORE,@MANUALE)
			ELSE 
				UPDATE GRADUATORIA_PROGETTI_DETTAGLIO SET PUNTEGGIO=@PUNTEGGIO_NEW,DATA_VALUTAZIONE=GETDATE(),OPERATORE=@OPERATORE
					WHERE ID_PRIORITA=@ID_PRIORITA AND ID_PROGETTO=@ID_PROGETTO
			--SELECT @ID_PRIORITA,@ID_PROGETTO,@PUNTEGGIO_NEW,GETDATE(),@OPERATORE,@MANUALE
			FETCH NEXT FROM CPRIORITA INTO @ID_PRIORITA,@MANUALE,@EVAL,@PUNTEGGIO_OLD,@PESO
		END
		CLOSE CPRIORITA
		DEALLOCATE CPRIORITA
		
		IF @COUNTER=0 RAISERROR('Attenzione! Non sono state associate le priorità necessarie al calcolo del punteggio.',13,1)
		ELSE BEGIN
			--SE IL PROGETTO E' IN STATO AMMISSIBILE SALVO IN GRADUATORIA PROGETTI
			DECLARE @ID_BANDO INT,@COD_STATO CHAR(1)
			SELECT @ID_BANDO=B.ID_BANDO,@COD_STATO=P.COD_STATO,@PUNTEGGIO_OLD=G.PUNTEGGIO,@PUNTEGGIO_NEW=SUM(D.PUNTEGGIO) 
			FROM BANDO B INNER JOIN vPROGETTO P ON B.ID_BANDO=P.ID_BANDO
			LEFT JOIN GRADUATORIA_PROGETTI G ON B.ID_BANDO=G.ID_BANDO AND P.ID_PROGETTO=G.ID_PROGETTO
			LEFT JOIN GRADUATORIA_PROGETTI_DETTAGLIO D ON P.ID_PROGETTO=D.ID_PROGETTO
			WHERE P.ID_PROGETTO=@ID_PROGETTO GROUP BY B.ID_BANDO,P.COD_STATO,G.PUNTEGGIO
			IF @COD_STATO='A' BEGIN
				IF @PUNTEGGIO_OLD IS NULL 
					INSERT INTO GRADUATORIA_PROGETTI (ID_BANDO,ID_PROGETTO,PUNTEGGIO,DATA_VALUTAZIONE,OPERATORE)
					VALUES(@ID_BANDO,@ID_PROGETTO,ISNULL(@PUNTEGGIO_NEW,0),GETDATE(),@OPERATORE)			
				ELSE 
					UPDATE GRADUATORIA_PROGETTI SET PUNTEGGIO=ISNULL(@PUNTEGGIO_NEW,0),DATA_VALUTAZIONE=GETDATE(),OPERATORE=@OPERATORE
					WHERE ID_BANDO=@ID_BANDO AND ID_PROGETTO=@ID_PROGETTO			
			END
		END
	END
	-- CALCOLO IN ISTRUTTORIA VARIANTI
	ELSE BEGIN		
		DECLARE CPRIORITA CURSOR FOR 
		SELECT R.ID_PRIORITA,R.FLAG_MANUALE,R.EVAL,G.PUNTEGGIO,S.PESO FROM BANDO B INNER JOIN PROGETTO P ON B.ID_BANDO=P.ID_BANDO
		INNER JOIN SCHEDA_X_PRIORITA S ON B.ID_SCHEDA_VALUTAZIONE=S.ID_SCHEDA_VALUTAZIONE
		INNER JOIN PRIORITA R ON S.ID_PRIORITA=R.ID_PRIORITA
		LEFT JOIN VARIANTI_X_PRIORITA G ON S.ID_PRIORITA=G.ID_PRIORITA AND P.ID_PROGETTO=G.ID_PROGETTO AND G.ID_VARIANTE=@ID_VARIANTE
		WHERE P.ID_PROGETTO=@ID_PROGETTO AND S.PESO>0 ORDER BY ORDINE
		
		SET @COUNTER=0
		OPEN CPRIORITA
		FETCH NEXT FROM CPRIORITA INTO @ID_PRIORITA,@MANUALE,@EVAL,@PUNTEGGIO_OLD,@PESO
		WHILE @@FETCH_STATUS=0 BEGIN
			SET @PUNTEGGIO_NEW=0
			SET @COUNTER=@COUNTER+1	
			IF @MANUALE=1 BEGIN
				-- INIZIALIZZO
				SET @INDICE_ID_PRIORITA=CHARINDEX(CAST(@ID_PRIORITA AS VARCHAR(18))+':',@PUNTEGGI_MANUALI)
				IF @INDICE_ID_PRIORITA>0 BEGIN				
					SET @INDICE_DUEPUNTI_SUCCESSIVI=CHARINDEX(':',@PUNTEGGI_MANUALI,@INDICE_ID_PRIORITA)
					SET @INDICE_PIPE_SUCCESSIVA=CHARINDEX('|',@PUNTEGGI_MANUALI,@INDICE_ID_PRIORITA)
					IF @INDICE_DUEPUNTI_SUCCESSIVI>0 AND @INDICE_PIPE_SUCCESSIVA>0 BEGIN
						BEGIN TRY
							SET @PUNTEGGIO_NEW=CAST(REPLACE(REPLACE(SUBSTRING(@PUNTEGGI_MANUALI,@INDICE_DUEPUNTI_SUCCESSIVI+1,
								@INDICE_PIPE_SUCCESSIVA-@INDICE_DUEPUNTI_SUCCESSIVI-1),'.',''),',','.') AS DECIMAL(10,6))
						END TRY
						BEGIN CATCH
							SET @PUNTEGGIO_NEW=0
						END CATCH
					END
				END
			END
			ELSE IF @EVAL IS NOT NULL BEGIN
				BEGIN TRY
					INSERT @PUNTEGGIO_TEMP
					EXEC sp_executesql @EVAL,N'@IdProgetto INT,@IdVariante INT,@FASE_ISTRUTTORIA BIT',@ID_PROGETTO,@ID_VARIANTE,@ISTRUTTORIA
					SELECT TOP 1 @PUNTEGGIO_NEW=PUNTEGGIO_TEMPORANEO FROM @PUNTEGGIO_TEMP
					DELETE FROM @PUNTEGGIO_TEMP
				END TRY
				BEGIN CATCH
					SET @PUNTEGGIO_NEW=0
				END CATCH
				-- SOLO LE PRIORITA CALCOLATE VENGONO PESATE
				SET @PUNTEGGIO_NEW=ISNULL(@PUNTEGGIO_NEW*@PESO,0)
			END
			
			IF @PUNTEGGIO_OLD IS NULL
				INSERT INTO VARIANTI_X_PRIORITA (ID_VARIANTE,ID_PRIORITA,ID_PROGETTO,PUNTEGGIO,DATA_VALUTAZIONE,OPERATORE,FLAG_DEFINITIVO)
				VALUES(@ID_VARIANTE,@ID_PRIORITA,@ID_PROGETTO,@PUNTEGGIO_NEW,GETDATE(),@OPERATORE,0)
			ELSE 
				UPDATE VARIANTI_X_PRIORITA SET PUNTEGGIO=@PUNTEGGIO_NEW,DATA_VALUTAZIONE=GETDATE(),OPERATORE=@OPERATORE,
					FLAG_DEFINITIVO=0 WHERE ID_PRIORITA=@ID_PRIORITA AND ID_PROGETTO=@ID_PROGETTO AND ID_VARIANTE=@ID_VARIANTE				
			
			--SELECT @ID_PRIORITA,@MANUALE,@EVAL,@PUNTEGGIO_OLD,@PUNTEGGIO_NEW,@PESO
			FETCH NEXT FROM CPRIORITA INTO @ID_PRIORITA,@MANUALE,@EVAL,@PUNTEGGIO_OLD,@PESO
		END
		CLOSE CPRIORITA
		DEALLOCATE CPRIORITA
	END
