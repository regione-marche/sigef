CREATE PROCEDURE [dbo].[SpPsrCalcoloContributoContoInteressi]
(
	@ID_INVESTIMENTO INT,
	@FASE VARCHAR(10)
)
AS
	DECLARE @TASSO_ABBUONO DECIMAL(10,5),@MAX_IMPORTO DECIMAL(18,2),@AMMONTARE_MUTUO DECIMAL(18,2),@AMMONTARE_INVESTIMENTI DECIMAL(18,2),
		@TASSO_ATTUALIZZAZIONE DECIMAL(10,5),@ANNI INT,@ID_PROGETTO_CORRELATO INT,@ID_VARIANTE INT,@ID_DISP_ATTUATIVA INT,
		@ID_INVESTIMENTO_ORIGINALE INT
	SELECT TOP 1 @TASSO_ABBUONO=ISNULL(QUOTA_MAX,4),@MAX_IMPORTO=ISNULL(IMPORTO_MAX,0),@AMMONTARE_MUTUO=COSTO_INVESTIMENTO,
		@AMMONTARE_INVESTIMENTI=SPESE_GENERALI,@TASSO_ATTUALIZZAZIONE=QUOTA_CONTRIBUTO_RICHIESTO,@ANNI=QUANTITA,
		@ID_PROGETTO_CORRELATO=I.ID_PROGETTO,@ID_VARIANTE=I.ID_VARIANTE,@ID_DISP_ATTUATIVA=P.ID_BANDO,
		@ID_INVESTIMENTO_ORIGINALE=ID_INVESTIMENTO_ORIGINALE
	FROM PIANO_INVESTIMENTI I INNER JOIN PROGETTO P ON I.ID_PROGETTO=P.ID_PROGETTO
	INNER JOIN BANDO_TIPO_INVESTIMENTI B ON P.ID_BANDO=B.ID_BANDO AND B.COD_TIPO_INVESTIMENTO=2
	WHERE I.ID_INVESTIMENTO=@ID_INVESTIMENTO
	----------------------------------------------
	IF @@ROWCOUNT=0 RAISERROR('Il bando non prevede l`inserimento di questa tipologia di spesa.',13,1)
	ELSE BEGIN
		DECLARE @NUMERO_RATE_ANNUALI INT,@NUMERO_RATE_TOTALI INT,@PERIODICITA_RATA DECIMAL(10,5),@TASSO_RATA_NOMINALE DECIMAL(18,5),
			@INTERESSI_SINGOLA_RATA DECIMAL(18,5),@TASSO_NOMINALE_ATTUALIZZATO DECIMAL(18,5),@CONTRIBUTO DECIMAL(18,2)
		SET @NUMERO_RATE_ANNUALI=2 -- FISSO
		SET @NUMERO_RATE_TOTALI=@NUMERO_RATE_ANNUALI*@ANNI
		SET @PERIODICITA_RATA=12/@NUMERO_RATE_ANNUALI
		SET @TASSO_RATA_NOMINALE=100*(POWER(100+@TASSO_ABBUONO,@PERIODICITA_RATA/12)/POWER(100,@PERIODICITA_RATA/12)-1)
		SET @INTERESSI_SINGOLA_RATA=@AMMONTARE_MUTUO*
			((POWER(CAST(100+@TASSO_RATA_NOMINALE AS FLOAT(10)),@NUMERO_RATE_TOTALI)/POWER(CAST(100 AS FLOAT(10)),@NUMERO_RATE_TOTALI)*@TASSO_RATA_NOMINALE/100)/
			(POWER(CAST(100+@TASSO_RATA_NOMINALE AS FLOAT(10)),@NUMERO_RATE_TOTALI)/POWER(CAST(100 AS FLOAT(10)),@NUMERO_RATE_TOTALI)-1))
			-@AMMONTARE_MUTUO/@NUMERO_RATE_TOTALI
		SET @TASSO_NOMINALE_ATTUALIZZATO=100*(POWER(CAST(100+@TASSO_ATTUALIZZAZIONE AS FLOAT(10)),@PERIODICITA_RATA/12)/
			POWER(CAST(100 AS FLOAT(10)),@PERIODICITA_RATA/12)-1)
		SET @CONTRIBUTO=ROUND(@INTERESSI_SINGOLA_RATA*(
			(POWER(CAST(100+@TASSO_NOMINALE_ATTUALIZZATO AS FLOAT(10)),@NUMERO_RATE_TOTALI)/POWER(CAST(100 AS FLOAT(10)),@NUMERO_RATE_TOTALI)-1)/
			(@TASSO_NOMINALE_ATTUALIZZATO/100*POWER(CAST(100+@TASSO_NOMINALE_ATTUALIZZATO AS FLOAT(10)),@NUMERO_RATE_TOTALI-1)/
			POWER(CAST(100 AS FLOAT(10)),@NUMERO_RATE_TOTALI-1))),2)
		
		DECLARE @CONTRIBUTO_NON_TRONCATO DECIMAL(18,2),@COD_TRONCAMENTO CHAR(1)
		IF @MAX_IMPORTO>0 AND @CONTRIBUTO>@MAX_IMPORTO BEGIN
			SET @CONTRIBUTO_NON_TRONCATO=@CONTRIBUTO
			SET @CONTRIBUTO=@MAX_IMPORTO
			SET @COD_TRONCAMENTO='A' -- MASSIMALE		
		END
		
		IF @ID_INVESTIMENTO_ORIGINALE IS NOT NULL BEGIN
			DECLARE @CONTRIBUTO_RICHIESTO DECIMAL(18,2)
			IF @FASE='IDOMANDA' OR (SELECT COUNT(*) FROM VARIANTI WHERE ID_VARIANTE=@ID_VARIANTE AND COD_TIPO IN ('AR','VI'))>0
				SELECT @CONTRIBUTO_RICHIESTO=CONTRIBUTO_RICHIESTO FROM PIANO_INVESTIMENTI WHERE ID_INVESTIMENTO=@ID_INVESTIMENTO_ORIGINALE
			ELSE IF @FASE='IVARIANTE' AND (SELECT COUNT(*) FROM VARIANTI WHERE ID_VARIANTE=@ID_VARIANTE AND COD_TIPO IN ('AT','VA'))>0
				SELECT @CONTRIBUTO_RICHIESTO=dbo.controlloContributoInvestimentoRichiestoVariante(@ID_INVESTIMENTO)
			IF @CONTRIBUTO>@CONTRIBUTO_RICHIESTO BEGIN
				SET @CONTRIBUTO_NON_TRONCATO=@CONTRIBUTO
				SET @CONTRIBUTO=@CONTRIBUTO_RICHIESTO
				SET @COD_TRONCAMENTO='O' -- BREVETTO ORIGINALE
			END		
		END
		-- CALCOLO IL CONTRIBUTO DISPONIBILE DI MISURA
		DECLARE @CONTRIBUTO_DISPONIBILE_MISURA DECIMAL(18,2)
		IF (SELECT COUNT(*) FROM VARIANTI WHERE ID_VARIANTE=@ID_VARIANTE AND COD_TIPO IN ('AT','VA','VI'))>0 BEGIN
			SET @CONTRIBUTO_DISPONIBILE_MISURA=DBO.calcoloContributoRimanenteFinanziabileProgetto(@ID_PROGETTO_CORRELATO,
				@ID_VARIANTE,@ID_INVESTIMENTO)
			IF @CONTRIBUTO>@CONTRIBUTO_DISPONIBILE_MISURA BEGIN
				SET @CONTRIBUTO_NON_TRONCATO=@CONTRIBUTO
				SET @CONTRIBUTO=@CONTRIBUTO_DISPONIBILE_MISURA
				SET @COD_TRONCAMENTO='M' -- DOMANDA
			END
		END
		
		SELECT @CONTRIBUTO AS CONTRIBUTO,@CONTRIBUTO_NON_TRONCATO AS CONTRIBUTO_NON_TRONCATO,@COD_TRONCAMENTO AS COD_TRONCAMENTO		
	END
