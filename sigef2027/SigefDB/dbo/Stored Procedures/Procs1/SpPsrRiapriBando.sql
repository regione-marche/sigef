
CREATE PROCEDURE [dbo].[SpPsrRiapriBando]
(
	@ID_BANDO INT, 
	@ID_UTENTE INT,
	@ID_BANDO_NEW_OUT INT OUTPUT,
	@MESS_OUT VARCHAR(200) OUTPUT 
	 
)
AS	
BEGIN


DECLARE @DATA_ATTUALE DATETIME ,@ID_NUOVO_BANDO INT, @ID_MODELLO_DOMANDA_NEW INT,@ID_MODELLO_DOMANDA INT,
		 @ID_STORICO_NUOVO INT, @ID_INTEGRAZIONE_NUOVA INT
SET @DATA_ATTUALE=GETDATE()
	BEGIN TRY
		BEGIN TRAN NUOVO_BANDO
			-- COPIO BANDO
			INSERT INTO BANDO (COD_ENTE, DESCRIZIONE, PAROLE_CHIAVE, DATA_INSERIMENTO, DISPOSIZIONI_ATTUATIVE, ID_SCHEDA_VALUTAZIONE,
			ID_PROGRAMMAZIONE, MULTIPROGETTO, MULTIMISURA, INTERESSE_FILIERA,FASCICOLO_RICHIESTO, ABILITA_VALUTAZIONE, AGGREGAZIONE)
			SELECT COD_ENTE, DESCRIZIONE, PAROLE_CHIAVE, @DATA_ATTUALE, DISPOSIZIONI_ATTUATIVE, ID_SCHEDA_VALUTAZIONE,
			ID_PROGRAMMAZIONE, MULTIPROGETTO, MULTIMISURA, INTERESSE_FILIERA,FASCICOLO_RICHIESTO, ABILITA_VALUTAZIONE, AGGREGAZIONE
			FROM BANDO WHERE ID_BANDO = @ID_BANDO
			SELECT @ID_NUOVO_BANDO=SCOPE_IDENTITY()
			
			INSERT INTO BANDO_STORICO (ID_BANDO,COD_STATO,DATA,OPERATORE)
			VALUES (@ID_NUOVO_BANDO,'P',@DATA_ATTUALE,@ID_UTENTE)
			SELECT @ID_STORICO_NUOVO=SCOPE_IDENTITY()
			
			INSERT INTO BANDO_INTEGRAZIONI (ID_BANDO,DATA,OPERATORE,DATA_SCADENZA,IMPORTO,IMPORTO_DI_MISURA,QUOTA_RISERVA)
			SELECT @ID_NUOVO_BANDO, @DATA_ATTUALE, @ID_UTENTE, @DATA_ATTUALE,BI.IMPORTO, BI.IMPORTO_DI_MISURA,BI.QUOTA_RISERVA
			FROM BANDO_INTEGRAZIONI BI INNER JOIN BANDO B ON B.ID_INTEGRAZIONE_ULTIMA = BI.ID
			WHERE B.ID_BANDO = @ID_BANDO 			
			SELECT @ID_INTEGRAZIONE_NUOVA=SCOPE_IDENTITY()
			
			-- RESPONSABILI BANDO
			INSERT INTO BANDO_RESPONSABILI 
			SELECT @ID_NUOVO_BANDO,ID_UTENTE,PROVINCIA,ATTIVO,GETDATE (),@ID_UTENTE
			FROM BANDO_RESPONSABILI
			WHERE ID_BANDO = @ID_BANDO AND ATTIVO =1
			
			-- COMITATO DI VALUTAZIONE
			INSERT INTO BANDO_VALUTATORI
			SELECT @ID_NUOVO_BANDO, ID_UTENTE, PRESIDENTE,ATTIVO,GETDATE (),DATA_FINE, ORDINE
			FROM BANDO_VALUTATORI
			WHERE ID_BANDO = @ID_BANDO AND ATTIVO =1

			-- configurazioni generali
			--INSERT INTO BANDO_CONFIG 
			--SELECT @ID_NUOVO_BANDO,GRUPPO_CONFIG, TIPO_CONFIG, VALORE
			--FROM BANDO_CONFIG WHERE ID_BANDO =@ID_BANDO	
		
			-- TIPO INVESTIMENTI
			INSERT INTO BANDO_TIPO_INVESTIMENTI
			SELECT @ID_NUOVO_BANDO, COD_TIPO_INVESTIMENTO, IMPORTO_MAX, IMPORTO_MIN, QUOTA_MAX, NOTE, PREMIO
			FROM BANDO_TIPO_INVESTIMENTI WHERE ID_BANDO= @ID_BANDO 
			
			-- INTERVENTI
			INSERT INTO BANDO_PROGRAMMAZIONE
			SELECT @ID_NUOVO_BANDO,ID_PROGRAMMAZIONE, MISURA_PREVALENTE, ID_DISPOSIZIONI_ATTUATIVE
			FROM BANDO_PROGRAMMAZIONE WHERE ID_BANDO = @ID_BANDO AND MISURA_PREVALENTE =1
			
			-- MASSIMALI
			INSERT INTO BANDO_MASSIMALI 
			SELECT @ID_NUOVO_BANDO,ID_PROGRAMMAZIONE,QUOTA,IMPORTO 
			FROM BANDO_MASSIMALI WHERE ID_BANDO =@ID_BANDO 
			
			-- CHECKLIST
			INSERT INTO STEP_X_BANDO 
			SELECT @ID_NUOVO_BANDO , ID_CHECK_LIST, COD_FASE
			FROM STEP_X_BANDO WHERE ID_BANDO = @ID_BANDO
			
			-- RELAZIONE TECNICA
			INSERT INTO BANDO_RELAZIONE_TECNICA 
			SELECT @ID_NUOVO_BANDO,DESCRIZIONE,TITOLO,ORDINE 
			FROM BANDO_RELAZIONE_TECNICA WHERE ID_BANDO = @ID_BANDO
			
			-- BUSINESS PLAN
			INSERT INTO BUSINESS_PLAN  
			SELECT @ID_NUOVO_BANDO,ID_QUADRO,ORDINE FROM BUSINESS_PLAN 
			WHERE ID_BANDO = @ID_BANDO
			
			-- SETTORI PRODUTTIVI
			INSERT INTO BANDO_X_SETTORE_PRODUTTIVO 
			SELECT @ID_NUOVO_BANDO, ID_SETTORE_PRODUTTIVO, ID_PRIORITA_SETTORIALE
			FROM BANDO_X_SETTORE_PRODUTTIVO WHERE ID_BANDO =@ID_BANDO			
			
			-- CODIFICA /DETTAGLIO/ SPECIFICA
			INSERT INTO CODIFICA_INVESTIMENTO 
			SELECT @ID_NUOVO_BANDO,COD_TP,DESCRIZIONE,AIUTO_MINIMO,CODICE,IS_MAX, QUERY_SQL, AIUTO_MINIMO_ALTREFONTI, QUERY_SQL_ALTREFONTI
			FROM CODIFICA_INVESTIMENTO WHERE ID_BANDO =@ID_BANDO
			
			-- CODICI ATECO
			INSERT INTO BANDO_COMUNI_LOCALIZZAZIONI 
			SELECT @ID_NUOVO_BANDO,ID_COMUNE, EFFETTO_SU_CONTRIBUTO, IS_ATTIVO
			FROM BANDO_COMUNI_LOCALIZZAZIONI WHERE ID_BANDO =@ID_BANDO
			
			-- COMUNI PER LOCALIZZAZIONE
			INSERT INTO BANDO_ATECO2007 
			SELECT @ID_NUOVO_BANDO,ID_ATECO2007
			FROM BANDO_ATECO2007 WHERE ID_BANDO =@ID_BANDO	
			
			INSERT INTO DETTAGLIO_INVESTIMENTI (ID_CODIFICA_INVESTIMENTO,COD_DETTAGLIO,DESCRIZIONE,MOBILE,QUOTA_SPESE_GENERALI,
				RICHIEDI_PARTICELLA,ID_UDM)
			SELECT C2.ID_CODIFICA_INVESTIMENTO, D.COD_DETTAGLIO, D.DESCRIZIONE,
					D.MOBILE, D.QUOTA_SPESE_GENERALI, D.RICHIEDI_PARTICELLA ,D.ID_UDM
			FROM  DETTAGLIO_INVESTIMENTI D 
				INNER JOIN  CODIFICA_INVESTIMENTO C ON C.ID_CODIFICA_INVESTIMENTO=D.ID_CODIFICA_INVESTIMENTO
				INNER JOIN  CODIFICA_INVESTIMENTO C2 ON C2.ID_BANDO=@ID_NUOVO_BANDO AND C.CODICE=C2.CODICE
			WHERE C.ID_BANDO =@ID_BANDO
			
			INSERT INTO SPECIFICA_INVESTIMENTI
			SELECT D2.ID_DETTAGLIO_INVESTIMENTO,COD_SPECIFICA,S.DESCRIZIONE 
			FROM SPECIFICA_INVESTIMENTI S 
				INNER JOIN DETTAGLIO_INVESTIMENTI D ON D.ID_DETTAGLIO_INVESTIMENTO=S.ID_DETTAGLIO_INVESTIMENTO
				INNER JOIN CODIFICA_INVESTIMENTO C ON C.ID_CODIFICA_INVESTIMENTO=D.ID_CODIFICA_INVESTIMENTO
				INNER JOIN CODIFICA_INVESTIMENTO C2 ON C2.ID_BANDO=@ID_NUOVO_BANDO AND C.CODICE=C2.CODICE
				INNER JOIN DETTAGLIO_INVESTIMENTI D2 ON D2.ID_CODIFICA_INVESTIMENTO=C2.ID_CODIFICA_INVESTIMENTO AND D2.COD_DETTAGLIO=D.COD_DETTAGLIO
			WHERE C.ID_BANDO=@ID_BANDO
			
			-- MODELLO DOMANDA
			SELECT @ID_MODELLO_DOMANDA=ID_MODELLO_DOMANDA  FROM BANDO WHERE ID_BANDO = @ID_BANDO
			
			INSERT INTO MODELLO_DOMANDA 
			SELECT  @ID_NUOVO_BANDO , M.DEFINITIVO, M.OPERATORE, M.DENOMINAZIONE, M.DESCRIZIONE   
			FROM MODELLO_DOMANDA M WHERE ID_DOMANDA= @ID_MODELLO_DOMANDA  
			SET @ID_MODELLO_DOMANDA_NEW=SCOPE_IDENTITY()
			
			INSERT INTO QUADRI_X_DOMANDA 
			SELECT ID_QUADRO, @ID_MODELLO_DOMANDA_NEW, ORDINE FROM QUADRI_X_DOMANDA WHERE ID_DOMANDA = @ID_MODELLO_DOMANDA  
			
			INSERT INTO ALLEGATI_X_DOMANDA 
			SELECT ID_ALLEGATO, @ID_MODELLO_DOMANDA_NEW, ORDINE FROM ALLEGATI_X_DOMANDA WHERE ID_DOMANDA = @ID_MODELLO_DOMANDA
			
			INSERT INTO DICHIARAZIONI_X_DOMANDA  
			SELECT ID_DICHIARAZIONE , @ID_MODELLO_DOMANDA_NEW, ORDINE , OBBLIGATORIO FROM DICHIARAZIONI_X_DOMANDA WHERE ID_DOMANDA = @ID_MODELLO_DOMANDA
			
			UPDATE BANDO SET ID_STORICO_ULTIMO=@ID_STORICO_NUOVO, ID_INTEGRAZIONE_ULTIMA=@ID_INTEGRAZIONE_NUOVA,ID_MODELLO_DOMANDA =@ID_MODELLO_DOMANDA_NEW
			WHERE ID_BANDO=@ID_NUOVO_BANDO
			
			/* BANDO CONFIG */
			INSERT INTO BANDO_CONFIG
			SELECT  @ID_NUOVO_BANDO, COD_TIPO, VALORE, ATTIVO, GETDATE(), @ID_UTENTE, DATA_FINE, OPERATORE_FINE
			FROM BANDO_CONFIG
			WHERE (ID_BANDO = @ID_BANDO) AND DATA_FINE IS NULL AND ATTIVO =1
			
			/*BANDO_TIPO_PAGAMENTO*/
			INSERT INTO BANDO_TIPO_PAGAMENTO
			SELECT @ID_NUOVO_BANDO,COD_TIPO,QUOTA_MAX,QUOTA_MIN,IMPORTO_MAX,IMPORTO_MIN,NUMERO,COD_TIPO_POLIZZA
			FROM BANDO_TIPO_PAGAMENTO WHERE (ID_BANDO = @ID_BANDO)
			
			/* BANDO REQUISITI PAGAMENTO*/
			--INSERT INTO BANDO_REQUISITI_PAGAMENTO
			--SELECT @ID_NUOVO_BANDO,COD_TIPO ,ID_REQUISITO,OBBLIGATORIO,REQUISITO_DI_INSERIMENTO,REQUISITO_DI_CONTROLLO,ORDINE
			--FROM BANDO_REQUISITI_PAGAMENTO WHERE (ID_BANDO = @ID_BANDO)
			
			/*BANDO_TIPO_VARIANTI*/
			INSERT INTO BANDO_TIPO_VARIANTI
			SELECT @ID_NUOVO_BANDO,COD_TIPO,NUMERO_MASSIMO
			FROM BANDO_TIPO_VARIANTI  WHERE (ID_BANDO = @ID_BANDO)
			
			/* BANDO REQUISITI VARIANTE */
			--INSERT INTO BANDO_REQUISITI_PAGAMENTO
			--SELECT @ID_NUOVO_BANDO,COD_TIPO ,ID_REQUISITO,OBBLIGATORIO,REQUISITO_DI_PRESENTAZIONE,REQUISITO_DI_ISTRUTTORIA,ORDINE
			--FROM BANDO_REQUISITI_VARIANTE WHERE (ID_BANDO = @ID_BANDO)

		
		COMMIT TRAN NUOVO_BANDO			
		SET @ID_BANDO_NEW_OUT=@ID_NUOVO_BANDO
		
	END TRY
	BEGIN CATCH
		ROLLBACK TRAN NUOVO_BANDO
		 --SELECT @MESS_OUT= ERROR_MESSAGE();
		RAISERROR('Non è stato possibile riaprire il BANDO. Se il problema persiste contattare helpdesk.sigef@regione.marche.it.',13,1)
	END CATCH		
END

