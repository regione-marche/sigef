CREATE PROCEDURE [dbo].[SpAssegnaSanzioneDomandaPagamento]
(
	@ID_DOMANDA_PAGAMENTO INT,
	@COD_TIPO_SANZIONE VARCHAR(10),
	@OPERATORE VARCHAR(16)
)
AS
	-- INSERISCE LA TIPOLOGIA IN TABELLA SANZIONI PER OGNI PROGETTO CORRELATO FACENTE PARTE DEL PROGETTO
	-- LA CUI MISURA LA PREVEDA
	-- VALORE DI RITORNO: SE>0 NUMERO DI MISURE INTERESSATE DALLA SANZIONE SE=0 GIA' COMMINATA 
	-- SE -1 NESSUN PAGAMENTO RICHIESTO SULLE MISURE INTERESSATE
	-- ELSE ERRORE ALTRO
	/*
	DECLARE	@ID_DOMANDA_PAGAMENTO INT,@COD_TIPO_SANZIONE VARCHAR(10),@OPERATORE VARCHAR(16)
	SET @ID_DOMANDA_PAGAMENTO=219
	SET @OPERATORE =''
	SET @COD_TIPO_SANZIONE='prova-sanz'*/

	DECLARE @RETVAL INT,@NPROGETTI_CANDIDATI_X_SANZIONE INT,@NPROGETTI_NON_RICHIESTI INT,@NMISURE_SANZIONATE INT,
		@ID_PROGETTO_CORRELATO INT,@COD_TIPO_DPAGAMENTO CHAR(3),@MISURA VARCHAR(10)
	set @RETVAL=0
	SET @NPROGETTI_CANDIDATI_X_SANZIONE=0
	SET @NPROGETTI_NON_RICHIESTI=0
	SET @NMISURE_SANZIONATE=0

	DECLARE PROGETTI CURSOR FOR
		SELECT P.ID_PROGETTO,D.COD_TIPO,PG.CODICE FROM DOMANDA_DI_PAGAMENTO D 
		INNER JOIN PROGETTO P ON D.ID_PROGETTO=ISNULL(P.ID_PROG_INTEGRATO,P.ID_PROGETTO)
		INNER JOIN BANDO B ON P.ID_BANDO=B.ID_BANDO
		INNER JOIN zPROGRAMMAZIONE PG ON B.ID_PROGRAMMAZIONE=PG.ID
		INNER JOIN ZPROGRAMMAZIONE_SANZIONI S ON PG.ID=S.ID_PROGRAMMAZIONE AND ATTIVA=1
		WHERE D.ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO AND S.COD_SANZIONE=@COD_TIPO_SANZIONE
		UNION
		SELECT P.ID_PROGETTO,D.COD_TIPO,PG.CODICE FROM DOMANDA_DI_PAGAMENTO D 
		INNER JOIN PROGETTO P ON D.ID_PROGETTO=ISNULL(P.ID_PROG_INTEGRATO,P.ID_PROGETTO)
		INNER JOIN BANDO B ON P.ID_BANDO=B.ID_BANDO
		INNER JOIN zPROGRAMMAZIONE PG ON B.ID_PROGRAMMAZIONE=PG.ID
		INNER JOIN ZPROGRAMMAZIONE_SANZIONI S ON PG.ID_PADRE=S.ID_PROGRAMMAZIONE AND ATTIVA=1
		WHERE D.ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO AND S.COD_SANZIONE=@COD_TIPO_SANZIONE
	OPEN PROGETTI
	FETCH NEXT FROM PROGETTI INTO @ID_PROGETTO_CORRELATO,@COD_TIPO_DPAGAMENTO,@MISURA
	WHILE @@FETCH_STATUS=0 BEGIN
		SET @NPROGETTI_CANDIDATI_X_SANZIONE=@NPROGETTI_CANDIDATI_X_SANZIONE+1
		
		-- AGGIUNGO LA SANZIONE SOLO SE SONO STATI RICHIESTI PAGAMENTI PER QUELLA MISURA O SE E' IL PREMIO 112 A SALDO
		IF (SELECT COUNT(*) FROM PAGAMENTI_RICHIESTI PR INNER JOIN PIANO_INVESTIMENTI I ON PR.ID_INVESTIMENTO=I.ID_INVESTIMENTO 
				WHERE PR.ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO AND I.ID_PROGETTO=@ID_PROGETTO_CORRELATO)>0 OR	
			(@COD_TIPO_DPAGAMENTO='SLD' AND @MISURA='1.1.2.') BEGIN			
			--SE LA SANZIONE NON E' GIA' STATA COMMINATA LA INSERISCO
			IF(SELECT COUNT(*) FROM SANZIONI WHERE COD_TIPO=@COD_TIPO_SANZIONE AND ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO
				AND ID_PROGETTO=@ID_PROGETTO_CORRELATO)=0 BEGIN
				INSERT INTO SANZIONI (COD_TIPO,ID_DOMANDA_PAGAMENTO,ID_PROGETTO,DATA_APPLICAZIONE,OPERATORE,AMMONTARE)
					VALUES(@COD_TIPO_SANZIONE,@ID_DOMANDA_PAGAMENTO,@ID_PROGETTO_CORRELATO,GETDATE(),@OPERATORE,0)
				IF @@ROWCOUNT=1 SET @NMISURE_SANZIONATE=@NMISURE_SANZIONATE+1
				ELSE BEGIN
					SET @COD_TIPO_SANZIONE='SANZIONE_NON_INSERITA:'+@COD_TIPO_SANZIONE
					RAISERROR(@COD_TIPO_SANZIONE,13,1)
					RETURN
				END
			END		
		END
		ELSE SET @NPROGETTI_NON_RICHIESTI=@NPROGETTI_NON_RICHIESTI+1
		FETCH NEXT FROM PROGETTI INTO @ID_PROGETTO_CORRELATO,@COD_TIPO_DPAGAMENTO,@MISURA
	END
	CLOSE PROGETTI
	DEALLOCATE PROGETTI

	IF(@NPROGETTI_CANDIDATI_X_SANZIONE=0) 
		SET @RETVAL=-1 --LA DOMANDA DI AIUTO NON ATTIVA NESSUNA DELLE MISURE INTERESSATE DALLA SANZIONE
	ELSE IF(@NPROGETTI_NON_RICHIESTI=@NPROGETTI_CANDIDATI_X_SANZIONE) 
		SET @RETVAL=-2 --NESSUN PAGAMENTO RICHIESTO PER LE MISURE INTERESSATE DALLA SANZIONE
	ELSE IF(@NMISURE_SANZIONATE>0) SET @RETVAL=@NMISURE_SANZIONATE
	SELECT @RETVAL
