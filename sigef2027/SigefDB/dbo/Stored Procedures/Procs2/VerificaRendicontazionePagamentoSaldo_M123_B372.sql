CREATE PROCEDURE [dbo].[VerificaRendicontazionePagamentoSaldo_M123_B372]
(
	@ID_DOMANDA_PAGAMENTO INT
)
AS

--DECLARE @ID_DOMANDA_PAGAMENTO INT;SET @ID_DOMANDA_PAGAMENTO=251
--DEFINIZIONI
DECLARE @ID_VARIANTE INT,@ID_PROGETTO INT,@DATA_MODIFICA_PAGAMENTO DATETIME,@ID_INVESTIMENTO INT,
@IMPORTO_RENDICONTATO_ATTUALE DECIMAL(18,2),@CONTRIBUTO_RENDICONTATO_ATTUALE DECIMAL(18,2),@ID_FASCICOLO INT,
--ESITO VERIFICA
@TIPO_RISULTATO CHAR(1),@MESSAGGIO_RISULTATO VARCHAR(3000)
SET @TIPO_RISULTATO='S'
SET @MESSAGGIO_RISULTATO=''

--TROVO PROGETTO E ULTIMA VARIANTE RELATIVA A QUESTA DOMANDA DI PAGAMENTO
SELECT @DATA_MODIFICA_PAGAMENTO=DATA_MODIFICA,@ID_PROGETTO=D.ID_PROGETTO,@ID_FASCICOLO=ID_FASCICOLO FROM DOMANDA_DI_PAGAMENTO D
	INNER JOIN PROGETTO P ON D.ID_PROGETTO=P.ID_PROGETTO WHERE ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO
SELECT @ID_VARIANTE=MAX(ID_VARIANTE) FROM VARIANTI WHERE ID_PROGETTO=@ID_PROGETTO AND APPROVATA=1 AND ANNULLATA=0 
	AND DATA_MODIFICA<@DATA_MODIFICA_PAGAMENTO

--CREO PIANO INVESTIMENTI VIRTUALE
DECLARE @INVESTIMENTI_TEMP TABLE ([ID_INVESTIMENTO] [int] NOT NULL,
	[ID_PROGETTO] [int] NULL,
	[ID_PROGRAMMAZIONE] [int] NULL,
	[DESCRIZIONE] [varchar](2000) NULL,
	[DATA_VARIAZIONE] [datetime] NULL,
	[OPERATORE_VARIAZIONE] [varchar](16) NULL,
	[COD_TIPO_INVESTIMENTO] [int] NULL,
	[COD_STP] [char](2) NULL,
	[ID_UNITA_MISURA] [int] NULL,
	[QUANTITA] [decimal](10, 2) NULL,
	[COSTO_INVESTIMENTO] [decimal](15, 2) NULL,
	[SPESE_GENERALI] [decimal](15, 2) NULL,
	[CONTRIBUTO_RICHIESTO] [decimal](15, 2) NULL,
	[QUOTA_CONTRIBUTO_RICHIESTO] [decimal](10, 2) NULL,
	[TASSO_ABBUONO] [decimal](10, 2) NULL,
	[ID_SETTORE_PRODUTTIVO] [int] NULL,
	[ID_PRIORITA_SETTORIALE] [int] NULL,
	[ID_OBIETTIVO_MISURA] [int] NULL,
	[ID_CODIFICA_INVESTIMENTO] [int] NULL,
	[ID_DETTAGLIO_INVESTIMENTO] [int] NULL,
	[ID_SPECIFICA_INVESTIMENTO] [int] NULL,
	[AMMESSO] [bit] NULL,
	[ISTRUTTORE] [varchar](16) NULL,
	[ID_INVESTIMENTO_ORIGINALE] [int] NULL,
	[DATA_VALUTAZIONE] [datetime] NULL,
	[VALUTAZIONE_INVESTIMENTO] [ntext] NULL,
	[ID_VARIANTE] [int] NULL,
	[COD_VARIAZIONE] [char](1),
	[NON_COFINANZIATO] [bit] NULL, 
	IMPORTO_TOTALE_RENDICONTATO DECIMAL(18,2),
	CONTRIBUTO_TOTALE_RENDICONTATO DECIMAL(18,2))	

INSERT INTO @INVESTIMENTI_TEMP
SELECT I.*,	dbo.calcoloTotaleImportoRendicontatoInvestimento(I.ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO) AS IMPORTO_TOTALE_RENDICONTATO,
	dbo.calcoloTotaleContributoRendicontatoInvestimento(I.ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO) AS CONTRIBUTO_TOTALE_RENDICONTATO 
FROM PROGETTO P INNER JOIN PIANO_INVESTIMENTI I ON P.ID_PROGETTO=I.ID_PROGETTO
WHERE ISNULL(P.ID_PROG_INTEGRATO,P.ID_PROGETTO)=@ID_PROGETTO AND
	((@ID_VARIANTE IS NULL AND ID_INVESTIMENTO_ORIGINALE IS NOT NULL AND AMMESSO=1 AND ID_VARIANTE IS NULL) OR
	(ID_VARIANTE=@ID_VARIANTE AND ISNULL(COD_VARIAZIONE,'X')!='A')) ORDER BY COD_TIPO_INVESTIMENTO

/**********************************************************************************************************/
--INIZIO CONTROLLO STEP
------------------------------------------------------------------------------------------------
--123 a) verifica del raggiungimento del rendimento globale dell`impresa (ID_PRIORITA =116)
DECLARE @COSTO_INVESTIMENTI DECIMAL(18,2)
SELECT @COSTO_INVESTIMENTI=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP WHERE COD_TIPO_INVESTIMENTO=1

DECLARE @COSTO_INVESTIMENTI_PRIORITA DECIMAL(18,2)
IF @COSTO_INVESTIMENTI>0 BEGIN
	SELECT @COSTO_INVESTIMENTI_PRIORITA=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP
		WHERE ID_INVESTIMENTO IN (SELECT ID_INVESTIMENTO FROM PRIORITA_X_INVESTIMENTI WHERE ID_PRIORITA=116)
					
	IF @COSTO_INVESTIMENTI_PRIORITA<=(@COSTO_INVESTIMENTI/2) BEGIN
		SET @TIPO_RISULTATO='N'
		SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - Aumento del rendimento globale dell`azienda non verificato.'
	END
END

------------------------------------------------------------------------------------------------
-- Impianti fotovoltaici <30% del costo totale del Progetto
DECLARE @COSTO_FOTOVOLTAICO DECIMAL (20,2)
SELECT @COSTO_FOTOVOLTAICO = ISNULL( SUM (IMPORTO_TOTALE_RENDICONTATO) , 0)  
FROM @INVESTIMENTI_TEMP I 
	INNER JOIN SPECIFICA_INVESTIMENTI S ON I.ID_SPECIFICA_INVESTIMENTO = S.ID_SPECIFICA_INVESTIMENTO 
WHERE S.COD_SPECIFICA IN ('80')

 
IF(@COSTO_FOTOVOLTAICO > @COSTO_INVESTIMENTI * 0.30) BEGIN 
	SET @TIPO_RISULTATO='N'
	SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - L`ammontare rendicontato per gli impianti fotovoltaici non può essere > 30% del totale progetto.'
END

------------------------------------------------------------------------------------------------
-- Costo relativo al settore ORTOFRUTTICOLO > = 450.000,00 €
DECLARE @ORTOFRUTTA INT, @COSTO_ORTOFRUTTA DECIMAL(18,2)
DECLARE @ID_BANDO INT , @IMPRESA_NO_OP INT
SELECT @ID_BANDO = ID_BANDO FROM PROGETTO WHERE ID_PROGETTO = @ID_PROGETTO

IF @ID_BANDO = 372 BEGIN
	SELECT @ORTOFRUTTA = COUNT(*), @COSTO_ORTOFRUTTA=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP WHERE ID_SETTORE_PRODUTTIVO = 33
	IF(ISNULL(@ORTOFRUTTA,0) >0 AND @COSTO_ORTOFRUTTA< 450000)BEGIN 
		SET @TIPO_RISULTATO='N'
		SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - L`ammontare rendicontato per il settore ORTOFRUTTA non può essere inferiore a 450.000€ .'
	END
END
ELSE BEGIN

SELECT @IMPRESA_NO_OP = COUNT(ID_PRIORITA) FROM PRIORITA_X_PROGETTO WHERE ID_PROGETTO = @ID_PROGETTO and ID_PRIORITA = 43

IF ISNULL(@IMPRESA_NO_OP,0)=0 BEGIN  
	SELECT @ORTOFRUTTA = COUNT(*), @COSTO_ORTOFRUTTA=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP WHERE ID_SETTORE_PRODUTTIVO = 33
	IF(ISNULL(@ORTOFRUTTA,0) >0 AND @COSTO_ORTOFRUTTA< 450000)BEGIN 
		SET @TIPO_RISULTATO='N'
		SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - L`ammontare rendicontato per il settore ORTOFRUTTA non può essere inferiore a 450.000€ .'
	END
END

END


  
/*********************************************************************************************************/
IF @TIPO_RISULTATO='S' SET @MESSAGGIO_RISULTATO='VERIFICA DI AMMISSIBILITA DEGLI IMPORTI RENDICONTATI CONFERMATA'
ELSE SET @MESSAGGIO_RISULTATO='Si sono riscontrati i seguenti errori:'+@MESSAGGIO_RISULTATO
SELECT @TIPO_RISULTATO AS TIPO_RISULTATO, @MESSAGGIO_RISULTATO AS MESSAGGIO_RISULTATO
