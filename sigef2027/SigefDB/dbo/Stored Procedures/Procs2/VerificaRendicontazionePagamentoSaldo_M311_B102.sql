CREATE PROCEDURE [dbo].[VerificaRendicontazionePagamentoSaldo_M311_B102]
(
	@ID_DOMANDA_PAGAMENTO INT
)
AS
 
--DECLARE @ID_DOMANDA_PAGAMENTO INT;SET @ID_DOMANDA_PAGAMENTO=251
--DEFINIZIONI
DECLARE @ID_VARIANTE INT,@ID_PROGETTO INT,@DATA_MODIFICA_PAGAMENTO DATETIME,@ID_INVESTIMENTO INT,
@IMPORTO_RENDICONTATO_ATTUALE DECIMAL(18,2),@CONTRIBUTO_RENDICONTATO_ATTUALE DECIMAL(18,2),@ID_FASCICOLO INT,
--ESITO VERIFICA
@TIPO_RISULTATO CHAR(1),@MESSAGGIO_RISULTATO VARCHAR(3000)
SET @TIPO_RISULTATO='S'
SET @MESSAGGIO_RISULTATO=''

--TROVO PROGETTO E ULTIMA VARIANTE RELATIVA A QUESTA DOMANDA DI PAGAMENTO
SELECT @DATA_MODIFICA_PAGAMENTO=DATA_MODIFICA,@ID_PROGETTO=D.ID_PROGETTO,@ID_FASCICOLO=ID_FASCICOLO FROM DOMANDA_DI_PAGAMENTO D
	INNER JOIN PROGETTO P ON D.ID_PROGETTO=P.ID_PROGETTO WHERE ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO
SELECT @ID_VARIANTE=MAX(ID_VARIANTE) FROM VARIANTI WHERE ID_PROGETTO=@ID_PROGETTO AND APPROVATA=1 AND ANNULLATA=0 
	AND DATA_MODIFICA<@DATA_MODIFICA_PAGAMENTO

--CREO PIANO INVESTIMENTI VIRTUALE
DECLARE @INVESTIMENTI_TEMP TABLE ([ID_INVESTIMENTO] [int] NOT NULL,
	[ID_PROGETTO] [int] NULL,
	[ID_PROGRAMMAZIONE] [int] NULL,
	[DESCRIZIONE] [varchar](2000) NULL,
	[DATA_VARIAZIONE] [datetime] NULL,
	[OPERATORE_VARIAZIONE] [varchar](16) NULL,
	[COD_TIPO_INVESTIMENTO] [int] NULL,
	[COD_STP] [char](2) NULL,
	[ID_UNITA_MISURA] [int] NULL,
	[QUANTITA] [decimal](10, 2) NULL,
	[COSTO_INVESTIMENTO] [decimal](15, 2) NULL,
	[SPESE_GENERALI] [decimal](15, 2) NULL,
	[CONTRIBUTO_RICHIESTO] [decimal](15, 2) NULL,
	[QUOTA_CONTRIBUTO_RICHIESTO] [decimal](10, 2) NULL,
	[TASSO_ABBUONO] [decimal](10, 2) NULL,
	[ID_SETTORE_PRODUTTIVO] [int] NULL,
	[ID_PRIORITA_SETTORIALE] [int] NULL,
	[ID_OBIETTIVO_MISURA] [int] NULL,
	[ID_CODIFICA_INVESTIMENTO] [int] NULL,
	[ID_DETTAGLIO_INVESTIMENTO] [int] NULL,
	[ID_SPECIFICA_INVESTIMENTO] [int] NULL,
	[AMMESSO] [bit] NULL,
	[ISTRUTTORE] [varchar](16) NULL,
	[ID_INVESTIMENTO_ORIGINALE] [int] NULL,
	[DATA_VALUTAZIONE] [datetime] NULL,
	[VALUTAZIONE_INVESTIMENTO] [ntext] NULL,
	[ID_VARIANTE] [int] NULL,
	[COD_VARIAZIONE] [char](1), [NON_COFINANZIATO] [bit] NULL , 
	IMPORTO_TOTALE_RENDICONTATO DECIMAL(18,2),
	CONTRIBUTO_TOTALE_RENDICONTATO DECIMAL(18,2))	
	
INSERT INTO @INVESTIMENTI_TEMP
SELECT I.*,	dbo.calcoloTotaleImportoRendicontatoInvestimento(I.ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO) AS IMPORTO_TOTALE_RENDICONTATO,
	dbo.calcoloTotaleContributoRendicontatoInvestimento(I.ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO) AS CONTRIBUTO_TOTALE_RENDICONTATO 
FROM PROGETTO P INNER JOIN PIANO_INVESTIMENTI I ON P.ID_PROGETTO=I.ID_PROGETTO
WHERE ISNULL(P.ID_PROG_INTEGRATO,P.ID_PROGETTO)=@ID_PROGETTO AND
	((@ID_VARIANTE IS NULL AND ID_INVESTIMENTO_ORIGINALE IS NOT NULL AND AMMESSO=1 AND ID_VARIANTE IS NULL) OR
	(ID_VARIANTE=@ID_VARIANTE AND ISNULL(COD_VARIAZIONE,'X')!='A')) ORDER BY COD_TIPO_INVESTIMENTO

/*--CALCOLO GLI IMPORTI RENDICONTATI DEI PAGAMENTI
DECLARE INVESTIMENTI CURSOR FOR SELECT ID_INVESTIMENTO FROM @INVESTIMENTI_TEMP
OPEN INVESTIMENTI
FETCH NEXT FROM INVESTIMENTI INTO @ID_INVESTIMENTO
WHILE @@FETCH_STATUS=0 BEGIN
	SELECT @IMPORTO_RENDICONTATO_ATTUALE=IMPORTO_RICHIESTO,@CONTRIBUTO_RENDICONTATO_ATTUALE=CONTRIBUTO_RICHIESTO
	FROM PAGAMENTI_RICHIESTI WHERE ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO AND ID_INVESTIMENTO=@ID_INVESTIMENTO
	UPDATE @INVESTIMENTI_TEMP SET 
	IMPORTO_TOTALE_RENDICONTATO=dbo.calcoloStoricoImportoRichiestoInvestimento(@ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO)+ISNULL(@IMPORTO_RENDICONTATO_ATTUALE,0),
	CONTRIBUTO_TOTALE_RENDICONTATO=dbo.calcoloStoricoContributoRichiestoInvestimento(@ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO)+ISNULL(@CONTRIBUTO_RENDICONTATO_ATTUALE,0)
	WHERE ID_INVESTIMENTO=@ID_INVESTIMENTO		
	FETCH NEXT FROM INVESTIMENTI INTO @ID_INVESTIMENTO
END
CLOSE INVESTIMENTI
DEALLOCATE INVESTIMENTI*/

/**********************************************************************************************************/
--INIZIO CONTROLLO STEP

-- Verifica sostenibilità investimento: rata annua reintegrazione < 40% PLV post-investimento (VIENE CALCOLATA LA PLV ANNO ATTUALE)

DECLARE @RATA_INTEGRAZIONE DECIMAL(10,2),@PLV DECIMAL(10,2)
SELECT @RATA_INTEGRAZIONE=SUM(CASE D.MOBILE WHEN 1 THEN IMPORTO_TOTALE_RENDICONTATO/10 ELSE IMPORTO_TOTALE_RENDICONTATO/30 END)
	FROM @INVESTIMENTI_TEMP I LEFT JOIN DETTAGLIO_INVESTIMENTI D ON I.ID_DETTAGLIO_INVESTIMENTO=D.ID_DETTAGLIO_INVESTIMENTO
	WHERE COD_TIPO_INVESTIMENTO=1
	
SELECT @PLV=SUM(PLV) FROM PLV_IMPRESA WHERE PREVISIONALE=0 AND ID_PROGETTO=@ID_PROGETTO AND ANNO--=YEAR(@DATA_MODIFICA_PAGAMENTO)
	IN (SELECT MAX(ANNO) FROM PLV_IMPRESA WHERE PREVISIONALE=0 AND ID_PROGETTO=@ID_PROGETTO)
IF @PLV IS NULL BEGIN
	SET @TIPO_RISULTATO='N'
	SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - E` necessario inserire la PLV post-investimento (anno del SALDO attuale).'
END
IF @RATA_INTEGRAZIONE>(ISNULL(@PLV,0)*0.4) BEGIN
	SET @TIPO_RISULTATO='N'
	SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - La rata annua di reintegrazione supera il 40% della PLV aziendale inserita.'
END

---------------------------------------------------------------------------------------------
-- VERIFICA PUNTEGGIO MINIMO = 0,08 (CRITERI B-I-J-K)

DECLARE @ID_SCHEDA_VALUTAZIONE INT,@COSTO_INVESTIMENTI DECIMAL(18,2),@PUNTEGGIO_MINIMO DECIMAL(18,6)

SELECT @ID_SCHEDA_VALUTAZIONE=ID_SCHEDA_VALUTAZIONE FROM BANDO B INNER JOIN PROGETTO P ON B.ID_BANDO = P.ID_BANDO 
	WHERE ID_PROGETTO=@ID_PROGETTO
	
SELECT @COSTO_INVESTIMENTI=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP WHERE COD_TIPO_INVESTIMENTO=1

-- Priorità B: investimenti destinati a migliorare i servizi agrituristici delle aziende (ID:29)
DECLARE @COSTO_INVESTIMENTI_SERVIZI DECIMAL(18,2),@COD_VALORE_PRIORITA_B VARCHAR(10)
DECLARE @PesoPrioritaB decimal(10,2),@ValoreMAXPrioritaB decimal(10,2),@PunteggioPrioritaB decimal(10,4)
SELECT @PesoPrioritaB=ISNULL(PESO,0),@ValoreMAXPrioritaB=ISNULL(VALORE_MAX,100) FROM vSCHEDA_X_PRIORITA 
	WHERE ID_PRIORITA=29 AND ID_SCHEDA_VALUTAZIONE = @ID_SCHEDA_VALUTAZIONE	

-- Calcolo gli investimenti per servizi
SELECT @COSTO_INVESTIMENTI_SERVIZI=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP
	WHERE COD_TIPO_INVESTIMENTO=1 AND ID_INVESTIMENTO IN (SELECT ID_INVESTIMENTO FROM PRIORITA_X_INVESTIMENTI WHERE ID_PRIORITA=29)

SET @PunteggioPrioritaB = 0
IF @COSTO_INVESTIMENTI_SERVIZI>0 BEGIN     	   		
	-- Numero finale strutture (ID PRIORITA: 59)				        	  
	SELECT @COD_VALORE_PRIORITA_B=CODICE FROM PRIORITA_X_PROGETTO PP INNER JOIN VALORI_PRIORITA VP ON PP.ID_VALORE = VP.ID_VALORE
		WHERE PP.ID_PROGETTO = @ID_PROGETTO AND PP.ID_PRIORITA = 59	  
		
	IF @COD_VALORE_PRIORITA_B IS NOT NULL BEGIN		   
		-- Quota investimenti per servizi > 30% del costo totale con 3 o più strutture
		IF (@COSTO_INVESTIMENTI_SERVIZI > (@COSTO_INVESTIMENTI * 0.3) AND @COD_VALORE_PRIORITA_B = '3') SET @PunteggioPrioritaB = 1
		-- Quota investimenti per servizi > 20% e < 30% sul costo totale con 3 o più strutture
		ELSE IF (@COSTO_INVESTIMENTI_SERVIZI > (@COSTO_INVESTIMENTI * 0.2) AND
			  @COSTO_INVESTIMENTI_SERVIZI <= (@COSTO_INVESTIMENTI * 0.3) AND @COD_VALORE_PRIORITA_B = '3') SET @PunteggioPrioritaB = 0.8
		-- Quota investimenti per servizi > 20% e < 30% sul costo totale e 2 strutture
		-- la quota deve essere > 20 %
		ELSE IF (@COSTO_INVESTIMENTI_SERVIZI > (@COSTO_INVESTIMENTI * 0.2)  AND @COD_VALORE_PRIORITA_B = '2') SET @PunteggioPrioritaB = 0.65
		-- Quota investimenti per servizi > 15% e < 20% sul costo totale con 3 o più strutture
		ELSE IF (@COSTO_INVESTIMENTI_SERVIZI > (@COSTO_INVESTIMENTI * 0.15) AND
			  @COSTO_INVESTIMENTI_SERVIZI <= (@COSTO_INVESTIMENTI * 0.2) AND @COD_VALORE_PRIORITA_B = '3') SET @PunteggioPrioritaB = 0.5
		-- Quota investimenti per servizi > 15% e < 20% sul costo totale con 2 strutture
		ELSE IF (@COSTO_INVESTIMENTI_SERVIZI > (@COSTO_INVESTIMENTI * 0.15) AND
			  @COSTO_INVESTIMENTI_SERVIZI <= (@COSTO_INVESTIMENTI * 0.2) AND @COD_VALORE_PRIORITA_B = '2') SET @PunteggioPrioritaB = 0.4
		-- Quota investimenti per servizi > 15% e 1 struttura
		ELSE IF (@COSTO_INVESTIMENTI_SERVIZI > (@COSTO_INVESTIMENTI * 0.15) AND @COD_VALORE_PRIORITA_B = '1') SET @PunteggioPrioritaB = 0.3
	END
END
SET @PunteggioPrioritaB = @PunteggioPrioritaB * @PesoPrioritaB / @ValoreMAXPrioritaB

------------------------------
-- Priorità I: Investimenti strutturali realizzati con tecniche di bioedilizia (ID:178)
DECLARE @PesoPrioritaI decimal(10,2),@ValoreMAXPrioritaI decimal(10,2),@ValorePrioritaI decimal(10,4),@PunteggioPrioritaI decimal(10,4),
		@InvestimentiFabbricati decimal(18,2),@InvestimentiBioedilizia DECIMAL(18,2)
  
SELECT @PesoPrioritaI=ISNULL(PESO,0),@ValoreMAXPrioritaI=ISNULL(VALORE_MAX,100) FROM vSCHEDA_X_PRIORITA 
	WHERE ID_PRIORITA=178 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
 
SET @InvestimentiFabbricati = (SELECT SUM(ISNULL(IMPORTO_TOTALE_RENDICONTATO ,0))
								FROM @INVESTIMENTI_TEMP I
									INNER JOIN vzPROGRAMMAZIONE PR ON PR.ID=I.ID_PROGRAMMAZIONE AND PR.CODICE IN ('3.1.1.a.A','3.1.1.a.B', '3.1.1.a.1')
								WHERE COD_TIPO_INVESTIMENTO=1 )
								
SET @InvestimentiBioedilizia = (SELECT SUM(ISNULL(IMPORTO_TOTALE_RENDICONTATO ,0))
								FROM @INVESTIMENTI_TEMP INV INNER JOIN 
								PRIORITA_X_INVESTIMENTI PXI ON INV.ID_INVESTIMENTO = PXI.ID_INVESTIMENTO AND ID_PRIORITA=177)								
IF (@InvestimentiFabbricati >=( 0.65 *@COSTO_INVESTIMENTI) )
BEGIN
    -- si considera il costo totale come costo totale sui fabbricati
		 -- Quota investimenti prioritari superiore all’80% sul costo totale
		 IF (@InvestimentiBioedilizia > (@InvestimentiFabbricati * 0.8) ) SET @ValorePrioritaI = 1

		 --Quota investimenti prioritari > 65% e < 80% sul costo totale
	     ELSE IF (@InvestimentiBioedilizia > (@InvestimentiFabbricati * 0.65) AND
				  @InvestimentiBioedilizia <= (@InvestimentiFabbricati * 0.8)) SET @ValorePrioritaI = 0.65

		 -- Quota investimenti prioritari > 50% e < 65% sul costo totale
	     ELSE IF (@InvestimentiBioedilizia > (@InvestimentiFabbricati * 0.50) AND
				  @InvestimentiBioedilizia <= (@InvestimentiFabbricati * 0.65) ) SET @ValorePrioritaI = 0.35
		--Quota investimenti prioritari inferiori al 50% sul costo totale
		ELSE SET @ValorePrioritaI = 0
     END
ELSE SET @ValorePrioritaI = 0	
SET @PunteggioPrioritaI = @ValorePrioritaI * @PesoPrioritaI / @ValoreMAXPrioritaI

-------------------------------
-- Priorità J: investimenti con riqualificazione architettonica riguardanti tutto il patrimonio aziendale ID: 31
DECLARE @PesoPrioritaJ decimal(10,2),@ValoreMAXPrioritaJ decimal(10,2),@ValorePrioritaJ decimal(10,4),
	@PunteggioPrioritaJ decimal(10,4)

SELECT @PesoPrioritaJ=ISNULL(PESO,0),@ValoreMAXPrioritaJ=ISNULL(VALORE_MAX,100) FROM vSCHEDA_X_PRIORITA 
		WHERE ID_PRIORITA=31 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
SELECT @ValorePrioritaJ=COUNT(ID_PRIORITA) FROM PRIORITA_X_PROGETTO WHERE ID_PRIORITA=31 AND ID_PROGETTO=@ID_PROGETTO
SET @PunteggioPrioritaJ = @ValorePrioritaJ * @PesoPrioritaJ / @ValoreMAXPrioritaJ

--------------------------------
-- Priorità K: investimenti destinati all`utilizzo di fonti energetiche rinnovabili ID: 62
DECLARE @PesoPrioritaK decimal(10,2),@ValoreMAXPrioritaK decimal(10,2),@ValorePrioritaK decimal(10,4),
	@PunteggioPrioritaK decimal(10,4)

SELECT @PesoPrioritaK=ISNULL(PESO,0),@ValoreMAXPrioritaK=ISNULL(VALORE_MAX,100) FROM vSCHEDA_X_PRIORITA 
		WHERE ID_PRIORITA=62 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
		
SELECT @ValorePrioritaK=CASE WHEN VP.CODICE = '1' THEN 0.8 WHEN VP.CODICE = '2' THEN 1 WHEN VP.CODICE = '3' THEN 0.4 
	WHEN VP.CODICE = '4' THEN 0.6 ELSE 0 END FROM PRIORITA_X_PROGETTO PP INNER JOIN VALORI_PRIORITA VP ON PP.ID_VALORE = VP.ID_VALORE
	WHERE PP.ID_PROGETTO=@ID_PROGETTO AND PP.ID_PRIORITA=62

SET @PunteggioPrioritaK = @ValorePrioritaK * @PesoPrioritaK / @ValoreMAXPrioritaK

SET @PUNTEGGIO_MINIMO=ISNULL(@PunteggioPrioritaB,0)+ISNULL(@PunteggioPrioritaI,0)+ISNULL(@PunteggioPrioritaJ,0)
	+ISNULL(@PunteggioPrioritaK,0)
IF @PUNTEGGIO_MINIMO<0.08 BEGIN
	SET @TIPO_RISULTATO='N'
	SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - Il punteggio calcolato per la Misura 311 sui criteri B-I-J-K non raggiunge il minimo di 0,08.'	
END

-----------------------------------------------------------------------------------------
-- MANTENIMENTO PUNTEGGIO MINIMO GRADUATORIA

DECLARE @MINIMO_GRADUATORIA_NON_FIN DECIMAL(10,5),@PUNTEGGIO_DOMANDA_IN_GRADUATORIA DECIMAL(10,5),@NUOVO_PUNTEGGIO_DOMANDA DECIMAL(10,5),
	@ID_BANDO INT
SELECT @ID_BANDO=P.ID_BANDO,@PUNTEGGIO_DOMANDA_IN_GRADUATORIA=PUNTEGGIO FROM PROGETTO P 
	INNER JOIN GRADUATORIA_PROGETTI G ON P.ID_PROGETTO=G.ID_PROGETTO WHERE P.ID_PROGETTO=@ID_PROGETTO
SELECT TOP 1 @MINIMO_GRADUATORIA_NON_FIN=PUNTEGGIO FROM GRADUATORIA_PROGETTI WHERE ID_BANDO=@ID_BANDO AND FINANZIABILITA='N' 
	ORDER BY PUNTEGGIO DESC

IF ISNULL(@MINIMO_GRADUATORIA_NON_FIN,0)>0 BEGIN

	-- PRIORITA A  resta invariato prendo valore dalla graduatoria - ID_PRIORITA = 56
	DECLARE @PunteggioPrioritaA DECIMAL(10,2),@PesoPrioritaA DECIMAL(10,2),@ValorePrioritaA DECIMAL(10,2)
	SELECT @PesoPrioritaA=ISNULL(PESO,0) FROM vSCHEDA_X_PRIORITA WHERE ID_PRIORITA=56 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
	SELECT @ValorePrioritaA=VP.PUNTEGGIO FROM PRIORITA_X_PROGETTO PP INNER JOIN VALORI_PRIORITA VP ON PP.ID_VALORE=VP.ID_VALORE
		WHERE PP.ID_PROGETTO=@ID_PROGETTO AND PP.ID_PRIORITA=56
	SET @PunteggioPrioritaA = @ValorePrioritaA * @PesoPrioritaA
	
	-- PRIORITA C NON IMPLEMENTATA NON SERVE
	
	-- PRIORITA D  Investimenti realizzati in aree Natura 2000 ID_PRIORITA = 25
	DECLARE @PesoPrioritaD DECIMAL(10,2),@ValorePrioritaD DECIMAL(10,2),@PunteggioPrioritaD DECIMAL(10,2)
	SELECT @PesoPrioritaD=ISNULL(PESO,0) FROM vSCHEDA_X_PRIORITA WHERE ID_PRIORITA=25 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
	IF((SELECT COUNT(I.ID_INVESTIMENTO) FROM PRIORITA_X_INVESTIMENTI PXI INNER JOIN @INVESTIMENTI_TEMP I 
		ON PXI.ID_INVESTIMENTO=I.ID_INVESTIMENTO WHERE ID_PRIORITA = 25)>0) SET @ValorePrioritaD=1				
	SET @PunteggioPrioritaD = ISNULL(@ValorePrioritaD,0) * @PesoPrioritaD
	
	--PRIORITA E Investimenti realizzati da aziende con offerta integrata di ricettività e ristorazione  ID_PRIORITA =  61
	DECLARE @PunteggioPrioritaE DECIMAL(10,2),@PesoPrioritaE DECIMAL(10,2),@ValorePrioritaE DECIMAL(10,2)
	SELECT @PesoPrioritaE=ISNULL(PESO,0) FROM vSCHEDA_X_PRIORITA WHERE ID_PRIORITA=61 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
	SELECT @ValorePrioritaE=CASE WHEN VP.CODICE='1' THEN 1 WHEN VP.CODICE='2' THEN 0.85 WHEN VP.CODICE='3' THEN 0.55 
		WHEN VP.CODICE='4' THEN 0.4 ELSE 0 END FROM PRIORITA_X_PROGETTO PP INNER JOIN VALORI_PRIORITA VP ON PP.ID_VALORE=VP.ID_VALORE
		WHERE PP.ID_PROGETTO = @ID_PROGETTO AND PP.ID_PRIORITA = 61
	SET @PunteggioPrioritaE = @ValorePrioritaE * @PesoPrioritaE
	
	-- PRIORITA F.	Investimenti realizzati da imprenditrici	
	DECLARE @PesoPrioritaF DECIMAL(10,2),@ValorePrioritaF DECIMAL(10,2),@PunteggioPrioritaF DECIMAL(10,2)
	SELECT @PesoPrioritaF=ISNULL(PESO,0) FROM vSCHEDA_X_PRIORITA WHERE ID_PRIORITA=26 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
	SELECT @ValorePrioritaF=COUNT(ID_PRIORITA) FROM PRIORITA_X_PROGETTO WHERE ID_PRIORITA = 26 AND ID_PROGETTO=@ID_PROGETTO		
	SET @PunteggioPrioritaF = @ValorePrioritaF * @PesoPrioritaF
	
	-- PRIORITA G Investimenti realizzati da aziende biologiche  ID_PRIORITA = 32	
	DECLARE @PunteggioPrioritaG DECIMAL(10,2),@PesoPrioritaG DECIMAL(10,2),@ValorePrioritaG DECIMAL(10,2)
	SELECT @PesoPrioritaG=ISNULL(PESO,0) FROM vSCHEDA_X_PRIORITA WHERE ID_PRIORITA=32 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
	SELECT @ValorePrioritaG=CASE WHEN VP.CODICE='1' THEN 1 WHEN VP.CODICE='2' THEN 0.5 WHEN VP.CODICE='3' THEN 0.5 ELSE 0 END 
		FROM PRIORITA_X_PROGETTO PP INNER JOIN VALORI_PRIORITA VP ON PP.ID_VALORE=VP.ID_VALORE WHERE PP.ID_PROGETTO=@ID_PROGETTO 
		AND PP.ID_PRIORITA=32
	SET @PunteggioPrioritaG = @ValorePrioritaG * @PesoPrioritaG
	
	-- PRIORITA H Investimenti realizzati da giovani agricoltori IAP
	DECLARE @PunteggioPrioritaH DECIMAL(10,2),@PesoPrioritaH DECIMAL(10,2),@ValorePrioritaH DECIMAL(10,2)
	SELECT @PesoPrioritaH=ISNULL(PESO,0) FROM vSCHEDA_X_PRIORITA WHERE ID_PRIORITA=54 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
	SELECT @ValorePrioritaH=COUNT(ID_PRIORITA) FROM PRIORITA_X_PROGETTO WHERE ID_PRIORITA = 54 AND ID_PROGETTO=@ID_PROGETTO		
	SET @PunteggioPrioritaH = @ValorePrioritaH * @PesoPrioritaH

	-- PRIORITA L. Investimenti destinati a creare occupazione 
	DECLARE @oreante decimal(10,2), @orepost decimal(10,2), @OREINCREMENTO decimal(10,2)
	DECLARE @PunteggioPrioritaL DECIMAL(10,2),@PesoPrioritaL DECIMAL(10,2),@ValorePrioritaL DECIMAL(10,2)
	SELECT @PesoPrioritaL=ISNULL(PESO,0) FROM vSCHEDA_X_PRIORITA WHERE ID_PRIORITA=63 AND ID_SCHEDA_VALUTAZIONE=@ID_SCHEDA_VALUTAZIONE
	-- Calcolo delle ore totali per le attività connesse
	SET @ValorePrioritaL=0
	SET @oreante=(SELECT SUM(ISNULL(SAU,0) * ISNULL(ORE_UNITARIE,0)) FROM PLV_IMPRESA 
					WHERE ID_PROGETTO = @ID_PROGETTO AND PREVISIONALE = 0 AND ID_ATTIVITA_CONNESSA IS NOT NULL AND 
					ANNO IN (SELECT MIN(ANNO) FROM PLV_IMPRESA WHERE PREVISIONALE=0 AND ID_PROGETTO=@ID_PROGETTO))
	SET @orepost=(SELECT SUM(ISNULL(SAU,0) * ISNULL(ORE_UNITARIE,0)) FROM PLV_IMPRESA WHERE ID_PROGETTO = @ID_PROGETTO AND PREVISIONALE = 1 AND ID_ATTIVITA_CONNESSA IS NOT NULL)
	SET @OREINCREMENTO = ISNULL(@orepost,0) - ISNULL(@oreante,0)
		IF (@OREINCREMENTO >= 1600)SET @ValorePrioritaL = 1
		ELSE IF (@OREINCREMENTO >= 800)SET @ValorePrioritaL = 0.5	
	SET @PunteggioPrioritaL = @ValorePrioritaL * @PesoPrioritaL
	
	-- PUNTEGGIO FINALE (PUNTEGGIO B+I+J+K CALCOLATO PRIMA)
	SET @NUOVO_PUNTEGGIO_DOMANDA=ISNULL(@PUNTEGGIO_MINIMO,0)* 100+ISNULL(@PunteggioPrioritaA,0)+ISNULL(@PunteggioPrioritaD,0)
		+ISNULL(@PunteggioPrioritaE,0)+ISNULL(@PunteggioPrioritaF,0)+ISNULL(@PunteggioPrioritaG,0)+ISNULL(@PunteggioPrioritaH,0) +ISNULL(@PunteggioPrioritaL,0)
		
	IF(@NUOVO_PUNTEGGIO_DOMANDA<=@MINIMO_GRADUATORIA_NON_FIN AND @NUOVO_PUNTEGGIO_DOMANDA<@PUNTEGGIO_DOMANDA_IN_GRADUATORIA) BEGIN
		SET @TIPO_RISULTATO='N'
		SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - Mantenimento del punteggio minimo di graduatoria NON VERIFICATO.'
	END
END

/*********************************************************************************************************/
IF @TIPO_RISULTATO='S' SET @MESSAGGIO_RISULTATO='VERIFICA DI AMMISSIBILITA DEGLI IMPORTI RENDICONTATI CONFERMATA'
ELSE SET @MESSAGGIO_RISULTATO='Si sono riscontrati i seguenti errori:'+@MESSAGGIO_RISULTATO
SELECT @TIPO_RISULTATO AS TIPO_RISULTATO, @MESSAGGIO_RISULTATO AS MESSAGGIO_RISULTATO
