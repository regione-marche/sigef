CREATE FUNCTION [dbo].[calcoloImportoGarantitoFidejussioneMultimisura]
(
  @ID_DOMANDA_PAGAMENTO int 
)
RETURNS @TABELLA_RIEPILOGO TABLE (ID_PROGETTO INT,ID_PROGRAMMAZIONE INT,CONTRIBUTO_RICHIESTO DECIMAL(18,5),ANTICIPO_PERCEPITO DECIMAL(18,5),
			ANTICIPO_RECUPERATO DECIMAL(18,5),ANTICIPO_DA_RECUPERARE DECIMAL(18,5),CONTRIBUTO_NETTO DECIMAL(18,5),
			COD_TIPO_INVESTIMENTO INT,IMPORTO_MAX_RECUPERO_ANTICIPO DECIMAL(18,5),QUOTA_MAX_RECUPERO_ANTICIPO DECIMAL(18,5), AMMONTARE_FIDEJUSSIONE  DECIMAL(18,5) )
AS
BEGIN 

	--DECLARE @ID_DOMANDA_PAGAMENTO INT
	--SET @ID_DOMANDA_PAGAMENTO=1274
	
	DECLARE @ID_PROGETTO INT,@COD_TIPO_DOMANDA_PAGAMENTO CHAR(3)
	SELECT @ID_PROGETTO=ID_PROGETTO,@COD_TIPO_DOMANDA_PAGAMENTO=COD_TIPO FROM DOMANDA_DI_PAGAMENTO 
	WHERE ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO	
	
	IF @COD_TIPO_DOMANDA_PAGAMENTO='ANT' BEGIN	
		INSERT INTO @TABELLA_RIEPILOGO		
		SELECT DISTINCT P.ID_PROGETTO,B.ID_PROGRAMMAZIONE,ISNULL(CONTRIBUTO_RICHIESTO,0) ,NULL,NULL,NULL,
			CASE WHEN ISNULL(CON.VALORE,110)>0 THEN  ISNULL(CONTRIBUTO_RICHIESTO,0) ELSE 0 end,NULL,NULL,NULL,ISNULL(CONTRIBUTO_RICHIESTO,0) * ISNULL(CON.VALORE, 110)/100
		FROM ANTICIPI_RICHIESTI A INNER JOIN BANDO B ON A.ID_BANDO=B.ID_BANDO
		INNER JOIN PROGETTO P ON B.ID_BANDO=P.ID_BANDO AND ISNULL(P.ID_PROG_INTEGRATO,ID_PROGETTO)=@ID_PROGETTO
		LEFT JOIN BANDO_CONFIG CON on CON.ID_BANDO = B.ID_BANDO AND CON.COD_TIPO ='QUOTIMPGARFID'  AND CON.ATTIVO =1 
		WHERE A.ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO
	END
	ELSE IF @COD_TIPO_DOMANDA_PAGAMENTO='SAL' BEGIN
		
		-- A SAL NON RICHIEDONO IL CONTO CAPITALE MIS 112 QUINDI NON LO CALCOLO
		INSERT INTO @TABELLA_RIEPILOGO
		SELECT P.ID_PROGETTO,B.ID_PROGRAMMAZIONE,SUM(R.CONTRIBUTO_RICHIESTO) ,NULL,NULL,NULL,NULL,BT.COD_TIPO_INVESTIMENTO,IMPORTO_MAX,QUOTA_MAX ,  SUM(R.CONTRIBUTO_RICHIESTO)*ISNULL(CON.VALORE, 110)/100
		FROM PROGETTO P INNER JOIN BANDO B ON P.ID_BANDO=B.ID_BANDO
		LEFT JOIN PIANO_INVESTIMENTI I ON P.ID_PROGETTO=I.ID_PROGETTO
		LEFT JOIN PAGAMENTI_RICHIESTI R ON I.ID_INVESTIMENTO=R.ID_INVESTIMENTO AND ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO
		LEFT JOIN BANDO_TIPO_INVESTIMENTI BT ON P.ID_BANDO=BT.ID_BANDO AND BT.COD_TIPO_INVESTIMENTO=8
		LEFT JOIN BANDO_CONFIG CON on CON.ID_BANDO = B.ID_BANDO AND CON.COD_TIPO ='QUOTIMPGARFID' AND CON.ATTIVO =1 
		WHERE ISNULL(P.ID_PROG_INTEGRATO,P.ID_PROGETTO)=@ID_PROGETTO
		GROUP BY P.ID_PROGETTO,B.ID_PROGRAMMAZIONE,BT.COD_TIPO_INVESTIMENTO,IMPORTO_MAX,QUOTA_MAX,CON.VALORE
				
		--CALCOLO L'ANTICIPO PERCEPITO E AGGIORNO LA QUOTA MAX DA % A €
		UPDATE T SET ANTICIPO_PERCEPITO=Q1.SOMMA,ANTICIPO_DA_RECUPERARE=Q1.SOMMA,QUOTA_MAX_RECUPERO_ANTICIPO=CASE
			WHEN COD_TIPO_INVESTIMENTO=8 THEN Q1.SOMMA*QUOTA_MAX_RECUPERO_ANTICIPO/100 END
		FROM @TABELLA_RIEPILOGO T
		INNER JOIN (
			SELECT E.ID_PROGETTO,SUM(IMPORTO_AMMISSIBILE) AS SOMMA FROM DOMANDA_DI_PAGAMENTO_ESPORTAZIONE E
			INNER JOIN DOMANDA_DI_PAGAMENTO D ON E.ID_DOMANDA_PAGAMENTO=D.ID_DOMANDA_PAGAMENTO
			WHERE D.ID_PROGETTO=@ID_PROGETTO AND D.ID_DOMANDA_PAGAMENTO<@ID_DOMANDA_PAGAMENTO AND D.ANNULLATA=0 AND D.APPROVATA=1
			AND D.COD_TIPO='ANT' GROUP BY E.ID_PROGETTO) AS Q1 ON T.ID_PROGETTO=Q1.ID_PROGETTO		
		
		-- SE E' STATO RICHIESTO UN ANTICIPO
		IF @@ROWCOUNT>0 BEGIN
			--CALCOLO L'ANTICIPO RECUPERATO IN DOMANDE DI PAGAMENTO PRECEDENTI
			UPDATE T SET ANTICIPO_RECUPERATO=Q1.SOMMA,ANTICIPO_DA_RECUPERARE=ISNULL(ANTICIPO_PERCEPITO,0)-ISNULL(Q1.SOMMA,0)
			FROM @TABELLA_RIEPILOGO T 
			INNER JOIN (
				SELECT E.ID_PROGETTO,SUM(IMPORTO_RECUPERO_ANTICIPO) AS SOMMA FROM DOMANDA_DI_PAGAMENTO_ESPORTAZIONE E
				INNER JOIN DOMANDA_DI_PAGAMENTO D ON E.ID_DOMANDA_PAGAMENTO=D.ID_DOMANDA_PAGAMENTO
				WHERE D.ID_PROGETTO=@ID_PROGETTO AND D.ID_DOMANDA_PAGAMENTO<@ID_DOMANDA_PAGAMENTO AND D.ANNULLATA=0 AND D.APPROVATA=1
				AND D.COD_TIPO!='ANT' GROUP BY E.ID_PROGETTO) AS Q1 ON T.ID_PROGETTO=Q1.ID_PROGETTO
		END		
		-- RECUPERO ANTICIPO A SAL SOLO SE PROFILATO NEL BANDO
		UPDATE @TABELLA_RIEPILOGO SET CONTRIBUTO_NETTO=CONTRIBUTO_RICHIESTO-CASE WHEN COD_TIPO_INVESTIMENTO=8 THEN 
			dbo.SIAR_MIN(ISNULL(ANTICIPO_DA_RECUPERARE,0),
				CASE WHEN IMPORTO_MAX_RECUPERO_ANTICIPO>0 AND QUOTA_MAX_RECUPERO_ANTICIPO>0 
					THEN dbo.SIAR_MIN(IMPORTO_MAX_RECUPERO_ANTICIPO,QUOTA_MAX_RECUPERO_ANTICIPO)
				ELSE dbo.SIAR_MAX(ISNULL(IMPORTO_MAX_RECUPERO_ANTICIPO,0),ISNULL(QUOTA_MAX_RECUPERO_ANTICIPO,0)) END)ELSE 0 END
		WHERE CONTRIBUTO_RICHIESTO IS NOT NULL
	END
RETURN 
END 

