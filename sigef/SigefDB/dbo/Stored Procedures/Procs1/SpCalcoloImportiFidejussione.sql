CREATE PROCEDURE [dbo].[SpCalcoloImportiFidejussione]
(
	@ID_PROGETTO INT, --ID DEL PROGETTO CORRELATO
	@FASE_ISTRUTTORIA BIT=0, --SE FASE ISTRUTTORIA TRUE, ALTRIMENTI DEVO CALCOLARE PER GLI INVESTIMENTI ORIGINALI
	@ID_VARIANTE INT=NULL,
	@IMPORTO_POLIZZA_INSERITO DECIMAL(18,2)=NULL
)
AS
	--DECLARE @ID_PROGETTO INT,@FASE_ISTRUTTORIA BIT,@ID_VARIANTE INT,@IMPORTO_POLIZZA_INSERITO DECIMAL(18,2)
	--SET @ID_PROGETTO=364
	--SET	@FASE_ISTRUTTORIA=1
	--SET @ID_VARIANTE=152
	--SET @IMPORTO_POLIZZA_INSERITO=2540

	-- RECUPERO I DATI DELLA POLIZZA FIDEJUSSORIA
	DECLARE @MAX_AIUTO DECIMAL(15,2),@MIN_AIUTO DECIMAL(15,2),@MAX_QUOTA DECIMAL(15,2),@COD_AIUTO INT,@COD_TRONCAMENTO CHAR(2)
	SELECT @COD_AIUTO=COD_TIPO_INVESTIMENTO,@MIN_AIUTO=ISNULL(IMPORTO_MIN,0),@MAX_AIUTO=ISNULL(IMPORTO_MAX,1000000000),
		@MAX_QUOTA=ISNULL(QUOTA_MAX,100)
	FROM PROGETTO P INNER JOIN BANDO_TIPO_INVESTIMENTI B ON P.ID_BANDO=B.ID_BANDO AND COD_TIPO_INVESTIMENTO=4
	WHERE P.ID_PROGETTO=@ID_PROGETTO

	IF @COD_AIUTO IS NULL RAISERROR('TIPOLOGIA DI INVESTIMENTO "POLIZZA FIDEJUSSORIA" NON ASSOCIATA AL BANDO',13,1)
	ELSE BEGIN
		DECLARE @COSTO_TOTALE DECIMAL(18,2),@SPESE_TECNICHE DECIMAL(18,2),@COSTO_BREVETTI DECIMAL(18,2),@MAX_SPESE_GENERALI DECIMAL(18,2),
			@AMMONTARE_POLIZZA DECIMAL(18,2),@AMMONTARE_SPESE_DISPONIBILE DECIMAL(18,2),@ID_INVESTIMENTO INT,@ID_INVESTIMENTO_ORIGINALE INT,
			@CONTRIBUTO_POLIZZA DECIMAL(18,2),@AMMONTARE_POLIZZA_ORIGINALE DECIMAL(18,2),@CONTRIBUTO_POLIZZA_ORIGINALE DECIMAL(18,2),
			@CONTRIBUTO_DISPONIBILE_MISURA DECIMAL(18,2)
			
		-- COSTO TOTALE DEGLI INVESTIMENTI	
		SELECT @COSTO_TOTALE=SUM(COSTO_INVESTIMENTO),@SPESE_TECNICHE=ROUND(SUM((CONTRIBUTO_RICHIESTO/QUOTA_CONTRIBUTO_RICHIESTO*100)- COSTO_INVESTIMENTO),2)
		FROM PIANO_INVESTIMENTI WHERE ID_PROGETTO=@ID_PROGETTO AND COD_TIPO_INVESTIMENTO=1 AND 		
			((@FASE_ISTRUTTORIA=0 AND ID_INVESTIMENTO_ORIGINALE IS NULL AND ID_VARIANTE IS NULL) OR @FASE_ISTRUTTORIA=1 AND 
			((@ID_VARIANTE IS NULL AND ID_VARIANTE IS NULL AND ID_INVESTIMENTO_ORIGINALE IS NOT NULL AND AMMESSO IS NOT NULL) OR
			(ID_VARIANTE=@ID_VARIANTE AND ISNULL(COD_VARIAZIONE,'X')!='A')))
		---------------------------------------------
		-- COSTO TOTALE DEI BREVETTI
		SELECT @COSTO_BREVETTI=SUM(COSTO_INVESTIMENTO) FROM PIANO_INVESTIMENTI WHERE ID_PROGETTO=@ID_PROGETTO AND COD_TIPO_INVESTIMENTO=5
			AND ((@FASE_ISTRUTTORIA=0 AND ID_INVESTIMENTO_ORIGINALE IS NULL AND ID_VARIANTE IS NULL) OR @FASE_ISTRUTTORIA=1 AND 
			((@ID_VARIANTE IS NULL AND ID_VARIANTE IS NULL AND ID_INVESTIMENTO_ORIGINALE IS NOT NULL AND AMMESSO IS NOT NULL) OR
			(ID_VARIANTE=@ID_VARIANTE AND ISNULL(COD_VARIAZIONE,'X')!='A')))
		---------------------------------------------
		-- PERCENTUALE MAX SPESE GENERALI 	
		SELECT @MAX_SPESE_GENERALI=ISNULL(QUOTA_MAX,100) FROM PROGETTO AS P
		INNER JOIN BANDO_TIPO_INVESTIMENTI AS B ON P.ID_BANDO=B.ID_BANDO AND COD_TIPO_INVESTIMENTO=6 WHERE P.ID_PROGETTO=@ID_PROGETTO
		---------------------------------------------	
		-- AMMONTARE DELLA POLIZZA, % SU COSTO TOTALE INVESTIMENTI
		SET @AMMONTARE_POLIZZA=@COSTO_TOTALE/100*@MAX_QUOTA
		-- CONTROLLO L'IMPORTO INSERITO
		IF @IMPORTO_POLIZZA_INSERITO IS NOT NULL BEGIN
			IF @IMPORTO_POLIZZA_INSERITO<0 SET @IMPORTO_POLIZZA_INSERITO=0
			IF @IMPORTO_POLIZZA_INSERITO<@AMMONTARE_POLIZZA SET @AMMONTARE_POLIZZA=@IMPORTO_POLIZZA_INSERITO
			ELSE IF @IMPORTO_POLIZZA_INSERITO>@AMMONTARE_POLIZZA SET @COD_TRONCAMENTO='DI' -- QUOTA MAX DA BANDO
		END
		
		IF @MAX_SPESE_GENERALI IS NOT NULL BEGIN
			-- RIMANENTE DEI SOLDI DISPONIBILI PER LE SPESE GENERALI 
			SET @AMMONTARE_SPESE_DISPONIBILE=@COSTO_TOTALE/100*@MAX_SPESE_GENERALI-@SPESE_TECNICHE-ISNULL(@COSTO_BREVETTI,0)
			IF(@AMMONTARE_SPESE_DISPONIBILE<0) SET @AMMONTARE_SPESE_DISPONIBILE=0
			IF(@AMMONTARE_SPESE_DISPONIBILE<@AMMONTARE_POLIZZA) BEGIN 
				SET @AMMONTARE_POLIZZA=@AMMONTARE_SPESE_DISPONIBILE
				SET @COD_TRONCAMENTO='DI' -- DISPONIBILE PER SPESE GENERALI
			END
		END

		-- SE LA POLIZZA E' GIA' PRESENTE SUL DB RITORNO L'ID PER AGGIORNARLO
		SELECT @ID_INVESTIMENTO=ID_INVESTIMENTO,@ID_INVESTIMENTO_ORIGINALE=ID_INVESTIMENTO_ORIGINALE FROM PIANO_INVESTIMENTI 
		WHERE ID_PROGETTO=@ID_PROGETTO AND COD_TIPO_INVESTIMENTO=4 AND
			((@FASE_ISTRUTTORIA=0 AND ID_INVESTIMENTO_ORIGINALE IS NULL AND ID_VARIANTE IS NULL) OR @FASE_ISTRUTTORIA=1 AND 
			((@ID_VARIANTE IS NULL AND ID_VARIANTE IS NULL AND ID_INVESTIMENTO_ORIGINALE IS NOT NULL AND AMMESSO IS NOT NULL) OR
			(ID_VARIANTE=@ID_VARIANTE AND ISNULL(COD_VARIAZIONE,'X')!='A')))	
		----------------------------------------------	
		SET @CONTRIBUTO_POLIZZA=0
		IF @AMMONTARE_POLIZZA>0 BEGIN		
			-- IMPOSTO COME MASSIMALI I VALORI DELL'ORIGINALE (SOLO PER FASE ISTRUTTORIA AMMISSIBILITA)
			IF(@FASE_ISTRUTTORIA=1) BEGIN
				IF @ID_INVESTIMENTO_ORIGINALE IS NOT NULL BEGIN
					SELECT @AMMONTARE_POLIZZA_ORIGINALE=COSTO_INVESTIMENTO,@CONTRIBUTO_POLIZZA_ORIGINALE=CONTRIBUTO_RICHIESTO 
					FROM PIANO_INVESTIMENTI WHERE ID_INVESTIMENTO=@ID_INVESTIMENTO_ORIGINALE
					IF(ISNULL(@AMMONTARE_POLIZZA_ORIGINALE,0)<@AMMONTARE_POLIZZA) BEGIN 
						SET @AMMONTARE_POLIZZA=ISNULL(@AMMONTARE_POLIZZA_ORIGINALE,0)
						SET @COD_TRONCAMENTO='IO' -- IMPORTO ORIGINALE
					END
				END
			END
			-- CALCOLO GLI INVESTIMENTI SU CUI SPALMARE IL COSTO DELLA POLIZZA				
			DECLARE @COSTO_INVESTIMENTO DECIMAL(18,2),@CONTRIBUTO DECIMAL(18,2),@PARTE_POLIZZA DECIMAL(18,2)
			DECLARE TOTALE CURSOR FOR SELECT COSTO_INVESTIMENTO,QUOTA_CONTRIBUTO_RICHIESTO FROM PIANO_INVESTIMENTI 
				WHERE ID_PROGETTO=@ID_PROGETTO AND COD_TIPO_INVESTIMENTO=1 AND
				((@FASE_ISTRUTTORIA=0 AND ID_INVESTIMENTO_ORIGINALE IS NULL AND ID_VARIANTE IS NULL) OR @FASE_ISTRUTTORIA=1 AND 
				((@ID_VARIANTE IS NULL AND ID_VARIANTE IS NULL AND ID_INVESTIMENTO_ORIGINALE IS NOT NULL AND AMMESSO IS NOT NULL) OR
				(ID_VARIANTE=@ID_VARIANTE AND ISNULL(COD_VARIAZIONE,'X')!='A')))
			OPEN TOTALE
			FETCH NEXT FROM TOTALE INTO @COSTO_INVESTIMENTO,@CONTRIBUTO
			WHILE @@FETCH_STATUS=0 BEGIN			
				SET @PARTE_POLIZZA=@AMMONTARE_POLIZZA*@COSTO_INVESTIMENTO/@COSTO_TOTALE
				SET @CONTRIBUTO_POLIZZA=@CONTRIBUTO_POLIZZA+@CONTRIBUTO*@PARTE_POLIZZA/100	
				FETCH NEXT FROM TOTALE INTO @COSTO_INVESTIMENTO,@CONTRIBUTO
			END
			CLOSE TOTALE
			DEALLOCATE TOTALE
			-------------------------------------------------------		
			IF @FASE_ISTRUTTORIA=1 BEGIN 
				DECLARE @COD_TIPO_VARIANTE CHAR(2)
				SELECT @COD_TIPO_VARIANTE=COD_TIPO FROM VARIANTI WHERE ID_VARIANTE=@ID_VARIANTE
				-- IL CONTRIBUTO NON PUO' SUPERARE QUELLO RICHIESTO
				IF(@COD_TIPO_VARIANTE!='AR' AND @COD_TIPO_VARIANTE!='VA' AND ISNULL(@CONTRIBUTO_POLIZZA_ORIGINALE,0)<@CONTRIBUTO_POLIZZA) BEGIN
					SET @CONTRIBUTO_POLIZZA=ISNULL(@CONTRIBUTO_POLIZZA_ORIGINALE,0)
					SET @COD_TRONCAMENTO='CO' -- CONTRIBUTO ORIGINALE
				END			
				-- CONTROLLO SUL RICHIESTO, PRIMA DELLE MODIFICHE DI ISTRUTTORIA, EVITA CHE L'AMMESSO PER SINGOLO INVESTIMENTO SIA SUPERIORE
				IF @COD_TIPO_VARIANTE IN ('AT','VA') BEGIN
					DECLARE @CONTRIBUTO_IN_PRESENTAZIONE DECIMAL(15,2)
					SELECT @CONTRIBUTO_IN_PRESENTAZIONE=dbo.controlloContributoInvestimentoRichiestoVariante(@ID_INVESTIMENTO)
					IF @CONTRIBUTO_POLIZZA>@CONTRIBUTO_IN_PRESENTAZIONE BEGIN
						SET @CONTRIBUTO_POLIZZA=@CONTRIBUTO_IN_PRESENTAZIONE
						SET @COD_TRONCAMENTO='CO'	
					END
				END						
				-- SOLO PER VARIANTI/A.T.
				IF(@ID_VARIANTE IS NOT NULL AND @COD_TIPO_VARIANTE!='AR') BEGIN
					SELECT @CONTRIBUTO_DISPONIBILE_MISURA=DBO.calcoloContributoRimanenteFinanziabileProgetto(@ID_PROGETTO,@ID_VARIANTE,@ID_INVESTIMENTO)				
					IF(@CONTRIBUTO_POLIZZA>@CONTRIBUTO_DISPONIBILE_MISURA) BEGIN
						SET @CONTRIBUTO_POLIZZA=@CONTRIBUTO_DISPONIBILE_MISURA
						SET @COD_TRONCAMENTO='FI' -- DISPONIBILE FINANZIABILE X MISURA
					END
				END
				/*
				ALTRI CONTROLLI SUI MASSIMALI DI CONTRIBUTO PER DOMANDA ED INTERVENTO
				PER ORA LI TRALASCIAMO PERCHE' NON ATTIVI
				*/
			END
		END
		------------------------------------------	
		SELECT ISNULL(@AMMONTARE_POLIZZA,0) AS AMMONTARE_POLIZZA,ISNULL(@CONTRIBUTO_POLIZZA,0) AS CONTRIBUTO_POLIZZA,@COD_TRONCAMENTO AS COD_TRONCAMENTO,
			QUOTA_CONTRIBUTO_POLIZZA=CASE WHEN @AMMONTARE_POLIZZA>0 THEN ROUND(@CONTRIBUTO_POLIZZA/@AMMONTARE_POLIZZA*100,2) ELSE 0 END,
			@ID_INVESTIMENTO AS ID_INVESTIMENTO
	END
