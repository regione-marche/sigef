CREATE PROCEDURE [dbo].[rptRegistroRitiri]
(
 @Programmazione INT = NULL,
 @IdProgetto INT = NULL,
 @DataInizio DATETIME = NULL,
 @DataFine DATETIME = NULL
) 
AS

DECLARE @IdTipoRecupero INT;
DECLARE @Recuperabile BIT;
DECLARE @Storico BIT;

SET @IdTipoRecupero = 28001;
SET @Recuperabile = 1;
SET @Storico = 0;

IF @DataInizio IS NULL
BEGIN
    SET @DataInizio = CONVERT(datetime, '20160601', 112);
END
IF @DataFine IS NULL
BEGIN
    SET @DataFine = CONVERT(datetime, '20300630', 112);
END

SELECT 
REC.ID_PROGETTO, 
COD_ASSE AS ASSE_INTERVENTO,
DESC_AZIONE AS TITOLO,
IMP.RAGIONE_SOCIALE AS BENEFICIARIO,
ISNULL(SOGGETTO_REVOCA_FINANZIAMENTO, '') AS SOGGETTO_REVOCA_FINANZIAMENTO,
ISNULL(CAST(AT.NUMERO AS VARCHAR) + '|' + CONVERT(VARCHAR, AT.DATA, 103) + '|' + CAST(AT.REGISTRO AS VARCHAR), '') AS ATTO_RITIRO,
CASE SEGNALAZIONE_OLAF
	WHEN 1 THEN 'Sì' 
	ELSE 'No'
END AS NOTIFICATO_OLAF,
CASE 
	WHEN CAST(DATA_AVVIO AS DATE) < CAST('01/07/' + CONVERT(VARCHAR, YEAR(DATA_AVVIO)) AS DATE) 
	THEN '01/07/' + CONVERT(VARCHAR, YEAR(DATA_AVVIO) - 1) + ' - ' + '30/06/' + CONVERT(VARCHAR, YEAR(DATA_AVVIO))
	ELSE '01/07/' + CONVERT(VARCHAR, YEAR(DATA_AVVIO)) + ' - ' + '30/06/' + CONVERT(VARCHAR, YEAR(DATA_AVVIO) +1)
END AS PERIODO_CONTABILE,
ISNULL(CONVERT(VARCHAR, DATA_CERTIFICAZIONE_RITIRO, 103), '') AS DATA_CERTIFICAZIONE_RITIRO,
ISNULL(REC.IMPORTO_DA_RECUPERARE_UE, 0) AS FESR,
CAST(ISNULL(REC.IMPORTO_DA_RECUPERARE_NAZIONALE * 0.7, 0.00) AS DECIMAL(15, 2)) AS STATO,
CAST(ISNULL(REC.IMPORTO_DA_RECUPERARE_NAZIONALE * 0.3, 0) AS DECIMAL(15, 2)) AS REGIONE,
ISNULL(REC.IMPORTO_DA_RECUPERARE_PUBBLICO, 0) AS TOTALE_PUBBLICO,
ISNULL(REC.IMPORTO_DA_RECUPERARE_PRIVATO, 0) AS PRIVATO,
ISNULL(REC.IMPORTO_DA_RECUPERARE_TOTALE, 0) AS TOTALE_SPESA_IRREGOLARE
FROM VREGISTRO_RECUPERO REC
INNER JOIN VIMPRESA IMP ON REC.ID_IMPRESA = IMP.ID_IMPRESA
LEFT JOIN ATTI AT ON REC.ID_ATTO_RITIRO = AT.ID_ATTO
LEFT JOIN REGISTRO_IRREGOLARITA IRR ON IRR.ID_IRREGOLARITA = REC.ID_REGISTRO_IRREGOLARITA
WHERE 1 = 1
AND (
	((@Programmazione IS NULL) OR REC.ID_PROGRAMMAZIONE = @Programmazione) AND 
	((@IdProgetto IS NULL) OR REC.ID_PROGETTO = @IdProgetto) AND 
	((@DataInizio IS NULL) OR DATA_AVVIO >= @DataInizio) AND 
	((@DataFine IS NULL) OR DATA_AVVIO <= @DataFine) AND 
	((@IdTipoRecupero IS NULL) OR ID_TIPO_RECUPERO = @IdTipoRecupero) AND 
	((@Recuperabile IS NULL) OR RECUPERABILE = @Recuperabile) AND 
	((@Storico IS NULL) OR STORICO = @Storico));
	
	
	
-- EXEC [rptRegistroRitiri] 45, null, null, null;