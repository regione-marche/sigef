CREATE PROCEDURE [dbo].[VerificaRendicontazionePagamentoSaldo_B305]
(
	@ID_DOMANDA_PAGAMENTO INT
)
AS

--DECLARE @ID_DOMANDA_PAGAMENTO INT;SET @ID_DOMANDA_PAGAMENTO=...
--DEFINIZIONI
DECLARE @ID_VARIANTE INT,@ID_PROGETTO INT,@DATA_MODIFICA_PAGAMENTO DATETIME,@ID_INVESTIMENTO INT,
@IMPORTO_RENDICONTATO_ATTUALE DECIMAL(18,2),@CONTRIBUTO_RENDICONTATO_ATTUALE DECIMAL(18,2),@ID_FASCICOLO INT,
--ESITO VERIFICA
@TIPO_RISULTATO CHAR(1),@MESSAGGIO_RISULTATO VARCHAR(3000)
SET @TIPO_RISULTATO='S'
SET @MESSAGGIO_RISULTATO=''

--TROVO PROGETTO E ULTIMA VARIANTE RELATIVA A QUESTA DOMANDA DI PAGAMENTO
SELECT @DATA_MODIFICA_PAGAMENTO=DATA_MODIFICA,@ID_PROGETTO=D.ID_PROGETTO,@ID_FASCICOLO=ID_FASCICOLO FROM DOMANDA_DI_PAGAMENTO D
	INNER JOIN PROGETTO P ON D.ID_PROGETTO=P.ID_PROGETTO WHERE ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO
SELECT @ID_VARIANTE=MAX(ID_VARIANTE) FROM VARIANTI WHERE ID_PROGETTO=@ID_PROGETTO AND APPROVATA=1 AND ANNULLATA=0 
	AND DATA_MODIFICA<@DATA_MODIFICA_PAGAMENTO

--CREO PIANO INVESTIMENTI VIRTUALE
DECLARE @INVESTIMENTI_TEMP TABLE ([ID_INVESTIMENTO] [int] NOT NULL,
	[ID_PROGETTO] [int] NULL,
	[ID_PROGRAMMAZIONE] [int] NULL,
	[DESCRIZIONE] [varchar](2000) NULL,
	[DATA_VARIAZIONE] [datetime] NULL,
	[OPERATORE_VARIAZIONE] [varchar](16) NULL,
	[COD_TIPO_INVESTIMENTO] [int] NULL,
	[COD_STP] [char](2) NULL,
	[ID_UNITA_MISURA] [int] NULL,
	[QUANTITA] [decimal](10, 2) NULL,
	[COSTO_INVESTIMENTO] [decimal](15, 2) NULL,
	[SPESE_GENERALI] [decimal](15, 2) NULL,
	[CONTRIBUTO_RICHIESTO] [decimal](15, 2) NULL,
	[QUOTA_CONTRIBUTO_RICHIESTO] [decimal](10, 2) NULL,
	[TASSO_ABBUONO] [decimal](10, 2) NULL,
	[ID_SETTORE_PRODUTTIVO] [int] NULL,
	[ID_PRIORITA_SETTORIALE] [int] NULL,
	[ID_OBIETTIVO_MISURA] [int] NULL,
	[ID_CODIFICA_INVESTIMENTO] [int] NULL,
	[ID_DETTAGLIO_INVESTIMENTO] [int] NULL,
	[ID_SPECIFICA_INVESTIMENTO] [int] NULL,
	[AMMESSO] [bit] NULL,
	[ISTRUTTORE] [varchar](16) NULL,
	[ID_INVESTIMENTO_ORIGINALE] [int] NULL,
	[DATA_VALUTAZIONE] [datetime] NULL,
	[VALUTAZIONE_INVESTIMENTO] [ntext] NULL,
	[ID_VARIANTE] [int] NULL,
	[COD_VARIAZIONE] [char](1),
	[NON_COFINANZIATO] [bit] NULL, 
	IMPORTO_TOTALE_RENDICONTATO DECIMAL(18,2),
	CONTRIBUTO_TOTALE_RENDICONTATO DECIMAL(18,2))	

INSERT INTO @INVESTIMENTI_TEMP
SELECT I.*,	dbo.calcoloTotaleImportoRendicontatoInvestimento(I.ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO) AS IMPORTO_TOTALE_RENDICONTATO,
	dbo.calcoloTotaleContributoRendicontatoInvestimento(I.ID_INVESTIMENTO,@ID_DOMANDA_PAGAMENTO) AS CONTRIBUTO_TOTALE_RENDICONTATO 
FROM PROGETTO P INNER JOIN PIANO_INVESTIMENTI I ON P.ID_PROGETTO=I.ID_PROGETTO
WHERE ISNULL(P.ID_PROG_INTEGRATO,P.ID_PROGETTO)=@ID_PROGETTO AND
	((@ID_VARIANTE IS NULL AND ID_INVESTIMENTO_ORIGINALE IS NOT NULL AND AMMESSO=1 AND ID_VARIANTE IS NULL) OR
	(ID_VARIANTE=@ID_VARIANTE AND ISNULL(COD_VARIAZIONE,'X')!='A')) ORDER BY COD_TIPO_INVESTIMENTO
	
/**********************************************************************************************************/
--INIZIO CONTROLLO STEP

---------------------------------------------------------------------------------------------------
DECLARE @CONTAC1 int, @CONTAC2 int, @CONTAC3 int, @CONTA_ARREDI int

DECLARE @COSTO_INVESTIMENTI DECIMAL(18,2)
SELECT @COSTO_INVESTIMENTI=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP WHERE COD_TIPO_INVESTIMENTO=1
----------------------------------------------------------------------------------------------------------------
-- Costo totale investimento 150.000 euro iva esclusa
IF (isnull(@COSTO_INVESTIMENTI,0) > 150000)BEGIN
	SET @TIPO_RISULTATO='N'
	SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - Costo totale investimento 150.000 euro iva esclusa'
END

---------------------------------------------------------------------------------------------------
--=== Studi e ricerche <= 20% del totale investimenti per gli interventi di recupero, restauro e riqualificazione. ==---
DECLARE @STUDI_RICERCHE DECIMAL(18,2), @RECUPERO_RESTAURO DECIMAL(18,2)
IF isnull(@COSTO_INVESTIMENTI,0)>0 BEGIN
	SELECT @STUDI_RICERCHE=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP
		WHERE ID_CODIFICA_INVESTIMENTO IN (SELECT ID_CODIFICA_INVESTIMENTO FROM CODIFICA_INVESTIMENTO WHERE ID_BANDO = 305 AND CODICE IN ('3a'))

	SELECT @RECUPERO_RESTAURO=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP
		WHERE ID_CODIFICA_INVESTIMENTO IN (SELECT ID_CODIFICA_INVESTIMENTO FROM CODIFICA_INVESTIMENTO WHERE ID_BANDO = 305 AND CODICE IN ('3b'))

	IF (@STUDI_RICERCHE > (@RECUPERO_RESTAURO * 0.20)) BEGIN
		SET @TIPO_RISULTATO='N'
		SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - Studi e ricerche <= 20% del totale investimenti per gli interventi di recupero, restauro e riqualificazione.'
	END
END

----------------------------------------------------------------------------------------------------
-- Spese tecniche max 10% costo interventi strutturali
DECLARE @SPESE_TECNICHE DECIMAL(18,2), @COSTI_STRUTTURALI DECIMAL(18,2)
IF isnull(@COSTO_INVESTIMENTI,0)>0 BEGIN
	SELECT @SPESE_TECNICHE=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP I 
		INNER JOIN CODIFICA_INVESTIMENTO CI on CI.ID_CODIFICA_INVESTIMENTO = I.ID_CODIFICA_INVESTIMENTO
		INNER JOIN DETTAGLIO_INVESTIMENTI DI on DI.ID_CODIFICA_INVESTIMENTO = CI.ID_CODIFICA_INVESTIMENTO AND 
								DI.ID_DETTAGLIO_INVESTIMENTO = I.ID_DETTAGLIO_INVESTIMENTO
						WHERE CI.ID_BANDO = 305 AND CI.CODICE = '3b' and DI.COD_DETTAGLIO in ('4')

	SELECT @COSTI_STRUTTURALI=SUM(IMPORTO_TOTALE_RENDICONTATO) FROM @INVESTIMENTI_TEMP I
		INNER JOIN CODIFICA_INVESTIMENTO CI on CI.ID_CODIFICA_INVESTIMENTO = I.ID_CODIFICA_INVESTIMENTO
		INNER JOIN DETTAGLIO_INVESTIMENTI DI on DI.ID_CODIFICA_INVESTIMENTO = CI.ID_CODIFICA_INVESTIMENTO AND 
								DI.ID_DETTAGLIO_INVESTIMENTO = I.ID_DETTAGLIO_INVESTIMENTO
						WHERE CI.ID_BANDO = 305 AND CI.CODICE = '3b' AND DI.COD_DETTAGLIO in ('1','2','3')
					
	IF isnull(@SPESE_TECNICHE,0)>(@COSTI_STRUTTURALI*0.10) BEGIN
		SET @TIPO_RISULTATO='N'
		SET @MESSAGGIO_RISULTATO=@MESSAGGIO_RISULTATO+'<BR /> - Spese tecniche max 10% costo interventi strutturali'
	END
END
----------------------------------------------------------------------------------------------------

/*********************************************************************************************************/
IF @TIPO_RISULTATO='S' SET @MESSAGGIO_RISULTATO='VERIFICA DI AMMISSIBILITA DEGLI IMPORTI RENDICONTATI CONFERMATA'
ELSE SET @MESSAGGIO_RISULTATO='Si sono riscontrati i seguenti errori:'+@MESSAGGIO_RISULTATO
SELECT @TIPO_RISULTATO AS TIPO_RISULTATO, @MESSAGGIO_RISULTATO AS MESSAGGIO_RISULTATO
