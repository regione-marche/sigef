
CREATE PROCEDURE [dbo].[SpGetPermessiOperatoreSuProgetto]
( 
	@ID_PROGETTO INT,
	@ID_UTENTE INT
)
AS
	 
	--DECLARE @ID_PROGETTO INT,@ID_UTENTE INT
	--SET @ID_PROGETTO=9065
	--SET @ID_UTENTE=419
	
	--0: NON ABILITATO  1: SOLA VISUALIZZAZIONE  2: ANCHE MODIFICA
	DECLARE @PERMESSO INT
	SET @PERMESSO=0
		 
	-- RECUPERO L'OPERATORE DI INSERIMENTO/RILASCIO DEL PROGETTO
	DECLARE @ID_OPERATORE_INSERIMENTO INT,@COD_ENTE VARCHAR(10),@PROVINCIA CHAR(2),@ID_PROFILO INT,@ID_IMPRESA INT,
		@COD_STATO_PROGETTO CHAR(1),@DATA_SCADENZA_BANDO DATETIME,@COD_ENTE_BANDO VARCHAR(10),@IMPRESA_ATTIVA BIT
	SELECT TOP 1 @ID_OPERATORE_INSERIMENTO=S.OPERATORE,@COD_ENTE=US.COD_ENTE,@PROVINCIA=PROVINCIA,@ID_PROFILO=ID_PROFILO,
		@ID_IMPRESA=I.ID_IMPRESA,@IMPRESA_ATTIVA=I.ATTIVA,@COD_STATO_PROGETTO=S.COD_STATO,@COD_ENTE_BANDO=B.COD_ENTE,
		@DATA_SCADENZA_BANDO=B.DATA_SCADENZA
	FROM PROGETTO P INNER JOIN PROGETTO_STORICO S ON P.ID_PROGETTO=S.ID_PROGETTO AND COD_STATO IN ('P','L')
	INNER JOIN vBANDO B ON P.ID_BANDO=B.ID_BANDO INNER JOIN vIMPRESA I ON P.ID_IMPRESA=I.ID_IMPRESA 
	INNER JOIN UTENTI_STORICO US ON S.OPERATORE=US.ID_UTENTE AND S.DATA>US.DATA
	WHERE P.ID_PROGETTO=@ID_PROGETTO ORDER BY S.DATA DESC,US.DATA DESC

	-- CONSULENTI E UTENTI CHE HANNO INSERITO LA DOMANDA (RAPP LEGALI O RUOLI CON POTERE DI FIRMA)
	IF @ID_OPERATORE_INSERIMENTO=@ID_UTENTE SET @PERMESSO=2	
	ELSE BEGIN
		-- RECUPERO L'OPERATORE CORRENTE
		DECLARE @ID_PF_OPERATORE_CORRENTE INT,@COD_ENTE_OPERATORE_CORRENTE VARCHAR(10),
			@COD_TIPO_ENTE_OPERATORE_CORRENTE VARCHAR(10),@PROVINCIA_OPERATORE_CORRENTE CHAR(2)
		SELECT @ID_PF_OPERATORE_CORRENTE=ID_PERSONA_FISICA,@COD_TIPO_ENTE_OPERATORE_CORRENTE=COD_TIPO_ENTE,
			@PROVINCIA_OPERATORE_CORRENTE=PROVINCIA,@COD_ENTE_OPERATORE_CORRENTE=COD_ENTE FROM vUTENTI WHERE ID_UTENTE=@ID_UTENTE
				
		-- COMPAGINE AZIENDALE (SOLO VISIONE, NON FACCIO MODIFICARE DA UN UTENTE DIVERSO DALL'UTENTE DI INSERIMENTO)
		IF (SELECT COUNT(*) FROM vPERSONE_X_IMPRESE WHERE ID_IMPRESA=@ID_IMPRESA AND ID_PERSONA=@ID_PF_OPERATORE_CORRENTE
			/*AND POTERE_DI_FIRMA=1*/ AND ATTIVO=1)>0 SET @PERMESSO=1
		-- REGIONALI O AMMINISTRATORI, ENTI IN CONSULTAZIONE
		ELSE IF @COD_TIPO_ENTE_OPERATORE_CORRENTE IN ('%','RM','ADC','EE') SET @PERMESSO=1
		-- CAA
		ELSE IF @COD_TIPO_ENTE_OPERATORE_CORRENTE='CAA' AND @COD_ENTE=@COD_ENTE_OPERATORE_CORRENTE BEGIN
			SET @PERMESSO=1
			IF(@PROVINCIA=@PROVINCIA_OPERATORE_CORRENTE OR @PROVINCIA_OPERATORE_CORRENTE IS NULL) SET @PERMESSO=2
		END
		-- CFS
		ELSE IF @COD_TIPO_ENTE_OPERATORE_CORRENTE='CFS' AND (SELECT COUNT(*) FROM MANDATI_IMPRESA WHERE ID_IMPRESA=@ID_IMPRESA
			AND TIPO_MANDATO='PSR' AND ATTIVO=1 AND COD_ENTE=@COD_ENTE_OPERATORE_CORRENTE)>0 SET @PERMESSO=1
		-- PER GAL O PROVINCE SOLA VISIONE SE IL BANDO E' IL LORO
		ELSE IF @COD_TIPO_ENTE_OPERATORE_CORRENTE IN ('GAL','PR') BEGIN 
			IF @COD_ENTE_BANDO=@COD_ENTE_OPERATORE_CORRENTE SET @PERMESSO=1
			ELSE IF (SELECT COUNT(*) FROM IMPRESA WHERE ID_IMPRESA=@ID_IMPRESA AND COD_ENTE=@COD_ENTE_OPERATORE_CORRENTE)>0 
				SET @PERMESSO=2
		END
		-- VALUTATORI DEL BANDO
		ELSE IF @COD_ENTE_OPERATORE_CORRENTE IS NULL BEGIN
			IF (SELECT COUNT(*) FROM BANDO_VALUTATORI V INNER JOIN PROGETTO P ON V.ID_BANDO=P.ID_BANDO
				WHERE P.ID_PROGETTO=@ID_PROGETTO AND V.ID_UTENTE=@ID_UTENTE AND V.ATTIVO=1)>0 SET @PERMESSO=1
		END
		-- PER ISTITUTI BANCARI E ALTRI ENTI SOLO VISUALIZZAZIONE SE HANNO IL MANDATO PSR
		ELSE IF (SELECT COUNT(*) FROM MANDATI_IMPRESA WHERE ID_IMPRESA=@ID_IMPRESA AND TIPO_MANDATO='PSR' AND ATTIVO=1 
			AND COD_ENTE=@COD_ENTE_OPERATORE_CORRENTE)>0 SET @PERMESSO=1
	END
	IF @PERMESSO=2 AND (@IMPRESA_ATTIVA=0 OR @COD_STATO_PROGETTO!='P'OR @DATA_SCADENZA_BANDO<GETDATE()) SET @PERMESSO=1
	SELECT @PERMESSO