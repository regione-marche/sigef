
CREATE PROCEDURE [dbo].[SpEstraiCampioneDomandeXRevisionePagamenti]
(
	@ID_LOTTO INT,
	@REVISORE VARCHAR(16)
)
AS
	--DECLARE @ID_LOTTO INT=2,@REVISORE VARCHAR(16)=''

	DECLARE @REVISIONE_CONCLUSA BIT, @NUMERO_ESTRAZIONI INT

	SELECT @NUMERO_ESTRAZIONI = NUMERO_ESTRAZIONI + 1,
		@REVISIONE_CONCLUSA = REVISIONE_CONCLUSA 
	FROM LOTTO_DI_REVISIONE 
	WHERE ID_LOTTO = @ID_LOTTO

	IF(@REVISIONE_CONCLUSA IS NOT NULL AND @REVISIONE_CONCLUSA = 1) BEGIN
		RAISERROR('Atenzione! Il controllo per il lotto selezionato è concluso, non è possibile estrarre ulteriormente il campione.', 13, 1)
	END
	ELSE BEGIN
		DECLARE @QUOTA_ESTRAZIONE DECIMAL(10, 6), @NR_DOMANDE_LOTTO DECIMAL (10, 5),@CAMPIONE INT

		SELECT @QUOTA_ESTRAZIONE = CAST(VALORE AS DECIMAL(10, 6)) 
		FROM BANDO_CONFIG C 
			JOIN LOTTO_DI_REVISIONE L ON C.ID_BANDO = L.ID_BANDO 
		WHERE 1 = 1
			AND C.COD_TIPO = 'QUOTESTRCAMPVALID' 
			AND L.ID_LOTTO=@ID_LOTTO

		SET @QUOTA_ESTRAZIONE = ISNULL(@QUOTA_ESTRAZIONE, 100) -- IMPONGO QUOTA PREVISTA DAL FESR

		SELECT @NR_DOMANDE_LOTTO = COUNT(ID_DOMANDA_PAGAMENTO) 
		FROM CTE_TESTATA_VALIDAZIONE 
		WHERE ID_LOTTO = @ID_LOTTO

		SET @CAMPIONE = CEILING(ISNULL(@NR_DOMANDE_LOTTO * @QUOTA_ESTRAZIONE / 100, 0))
		IF @CAMPIONE > @NR_DOMANDE_LOTTO 
			SET @CAMPIONE=@NR_DOMANDE_LOTTO

		DECLARE @ID_DOMANDA_PAGAMENTO INT,@COUNTER INT=0,@RAND_ID INT

		WHILE @COUNTER < @CAMPIONE BEGIN			
			SET @RAND_ID = RAND() * @NR_DOMANDE_LOTTO + 1
					
			SELECT @ID_DOMANDA_PAGAMENTO = MIN(ID_DOMANDA_PAGAMENTO) 
			FROM CTE_TESTATA_VALIDAZIONE 
			WHERE 1 = 1
				AND ID_LOTTO = @ID_LOTTO 
				AND ID_DOMANDA_PAGAMENTO IN (SELECT TOP(@RAND_ID) RD.ID_DOMANDA_PAGAMENTO 
											FROM CTE_TESTATA_VALIDAZIONE RD
												JOIN DOMANDA_DI_PAGAMENTO DP ON RD.ID_DOMANDA_PAGAMENTO = DP.ID_DOMANDA_PAGAMENTO
											WHERE 1 = 1
												AND ID_LOTTO = @ID_LOTTO 
												AND SELEZIONATA_X_REVISIONE = 0 
												AND DP.CF_ISTRUTTORE != @REVISORE 
											ORDER BY ID_DOMANDA_PAGAMENTO DESC)			

			UPDATE TESTATA_VALIDAZIONE 
			SET SELEZIONATA_X_REVISIONE = 1,
				NUMERO_ESTRAZIONE = @NUMERO_ESTRAZIONI
			WHERE 1 = 1
				AND ID_LOTTO = @ID_LOTTO 
				AND ID_DOMANDA_PAGAMENTO = @ID_DOMANDA_PAGAMENTO

			SET @COUNTER = @COUNTER+1
		END
		
		SELECT COUNT(*) 
		FROM CTE_TESTATA_VALIDAZIONE 
		WHERE 1 = 1
			AND ID_LOTTO = @ID_LOTTO 
			AND SELEZIONATA_X_REVISIONE = 1 
			AND NUMERO_ESTRAZIONE = @NUMERO_ESTRAZIONI
	END
GO


