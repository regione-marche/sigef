CREATE PROCEDURE [dbo].[RptDecretiVarianti] 
	@IdBando int, 
	@IdProgetto int =null,
	@Approvate char(1),  
	@NascondiDecretiInseriti bit
AS
BEGIN
	SET NOCOUNT ON;

	SELECT ISNULL(P.ID_PROG_INTEGRATO, P.ID_PROGETTO) AS ID_PROGETTO, 
	RAGIONE_SOCIALE, PR.DESCRIZIONE AS PSR, 
	PR.CODICE AS CODICE_MISURA, 
	VBA.DATA_SCADENZA, P.PROVINCIA_DI_PRESENTAZIONE, 
	V.SEGNATURA AS SEGNATURA_PRESENTAZIONE, V.ID_VARIANTE, 
	V.TIPO_VARIANTE, V.APPROVATA, V.DATA_MODIFICA, V.COD_TIPO,
	V.ANNULLATA, V.DATA_ANNULLAMENTO, V.SEGNATURA_ANNULLAMENTO, V.SEGNATURA_APPROVAZIONE, 
	A.NUMERO AS NUMERO_ATTO, A.DATA AS DATA_ATTO, A.DESCRIZIONE, 
	CASE WHEN VXP.FLAG_DEFINITIVO = 1 THEN VXP.PUNT ELSE 0 END AS PUNTEGGIO,
	V.DATA_APPROVAZIONE,
	VS.SEGNATURA AS SEGNATURA_COMUNICAZIONE, V.DESCRIZIONE AS DESC_VARIANTE

	FROM vVARIANTI AS V 
	INNER JOIN PROGETTO AS P ON V.ID_PROGETTO = P.ID_PROGETTO 
	INNER JOIN VBANDO AS VBA ON VBA.ID_BANDO = P.ID_BANDO 
	INNER JOIN VZPROGRAMMAZIONE PR ON VBA.ID_PROGRAMMAZIONE = PR.ID
	LEFT JOIN ATTI AS A ON A.ID_ATTO = V.ID_ATTO_APPROVAZIONE 
	LEFT OUTER JOIN (SELECT SUM(PUNTEGGIO) AS PUNT, ID_VARIANTE, FLAG_DEFINITIVO
					 FROM VARIANTI_X_PRIORITA
					 GROUP BY ID_VARIANTE, FLAG_DEFINITIVO) 
					 AS VXP ON VXP.ID_VARIANTE = V.ID_VARIANTE 
	LEFT OUTER JOIN VARIANTI_SEGNATURE AS VS ON VS.ID_VARIANTE = V.ID_VARIANTE AND VS.COD_TIPO = 'CEI'
	INNER JOIN VIMPRESA I ON I.ID_IMPRESA = P.ID_IMPRESA  
	WHERE P.ID_BANDO = @IdBando 
	AND ((@Approvate = 'T' AND V.APPROVATA IS NOT NULL) OR (@Approvate = 'A' AND V.APPROVATA = 1) OR (@Approvate = 'N' AND V.APPROVATA = 0)) 
	AND P.ID_PROGETTO = ISNULL(@IdProgetto, P.ID_PROGETTO)
	AND ((@NascondiDecretiInseriti = 1 AND V.ID_ATTO_APPROVAZIONE IS NULL) 
	OR @NascondiDecretiInseriti = 0)
	AND V.COD_TIPO='VA' 
	AND SEGNATURA_APPROVAZIONE IS NOT NULL 
	AND ANNULLATA=0 

	ORDER BY P.ID_PROGETTO
END
