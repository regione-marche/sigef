CREATE PROCEDURE [dbo].[SpCalcoloSanzioneVariazioneInvestimenti]
(
	@ID_SANZIONE INT,
	@DURATA DECIMAL(18,2) =NULL,
	@GRAVITA DECIMAL(18,2) =NULL,
	@ENTITA DECIMAL(18,2)=NULL
)
AS

/*
DECLARE @ID_SANZIONE INT,@DURATA DECIMAL(18,2),@GRAVITA DECIMAL(18,2),@ENTITA DECIMAL(18,2)
SET @ID_SANZIONE=93
SET @DURATA =6
SET @ENTITA=50000
SET @GRAVITA=4*/

IF(@DURATA IS NULL OR @ENTITA IS NULL OR @GRAVITA IS NULL) RAISERROR('INPUT_ERROR',13,1)
ELSE BEGIN
	DECLARE @ID_DOMANDA_PAGAMENTO INT,@ID_PROGETTO INT,@ID_VARIANTE INT, @SPESA_TOTALE_FINANZIATA DECIMAL(18,2),
		@DATA_MODIFICA_PAGAMENTO DATETIME, @ID_INVESTIMENTO INT,@PAGAMENTO_AMMESSO DECIMAL(18,2)
	
	SELECT TOP 1 @ID_DOMANDA_PAGAMENTO=S.ID_DOMANDA_PAGAMENTO,@ID_PROGETTO=D.ID_PROGETTO,@ID_VARIANTE=ID_VARIAZIONE_ACCERTATA,
	@DATA_MODIFICA_PAGAMENTO=DATA_MODIFICA,@ID_INVESTIMENTO=ID_INVESTIMENTO FROM SANZIONI S 
	INNER JOIN DOMANDA_DI_PAGAMENTO D ON S.ID_DOMANDA_PAGAMENTO=D.ID_DOMANDA_PAGAMENTO WHERE S.ID_SANZIONE=@ID_SANZIONE
	IF @ID_VARIANTE IS NULL
		SELECT @ID_VARIANTE=MAX(ID_VARIANTE) FROM VARIANTI WHERE ID_PROGETTO=@ID_PROGETTO AND APPROVATA=1 AND ANNULLATA=0 
		AND DATA_MODIFICA<@DATA_MODIFICA_PAGAMENTO		
		
	SET @SPESA_TOTALE_FINANZIATA= DBO.calcoloCostoTotaleProgetto(@ID_PROGETTO,1,@ID_VARIANTE)
	IF(@SPESA_TOTALE_FINANZIATA<=0)	RAISERROR('SPESA_FINANZIATA_ERROR',13,1)	
	ELSE BEGIN
		SELECT @PAGAMENTO_AMMESSO=CONTRIBUTO_AMMESSO FROM PAGAMENTI_RICHIESTI
		WHERE ID_DOMANDA_PAGAMENTO=@ID_DOMANDA_PAGAMENTO AND ID_INVESTIMENTO=@ID_INVESTIMENTO
		IF(@PAGAMENTO_AMMESSO IS NULL) RAISERROR('PAGAMENTO_NON_ISTRUITO_ERROR',13,1)
		ELSE BEGIN
			DECLARE @CLASSE_VIOLAZIONE_DURATA INT,@CLASSE_VIOLAZIONE_GRAVITA INT,@CLASSE_VIOLAZIONE_ENTITA INT,@QUOTA_ENTITA DECIMAL(10,2),
				@SOMMA_VIOLAZIONI DECIMAL(10,2),@QUOTA_RIDUZIONE DECIMAL(10,2)
			-- SELEZIONATO DALL'ISTRUTTORE
			SELECT @CLASSE_VIOLAZIONE_DURATA=CLASSE_VIOLAZIONE FROM TIPO_SANZIONI_PARAMETRI WHERE CODICE=@DURATA
			-- AMMONTARE ENTITA INPUTATO DALL'ISTRUTTORE
			SET @QUOTA_ENTITA =100*@ENTITA/@SPESA_TOTALE_FINANZIATA
			IF(@QUOTA_ENTITA>30)SET @CLASSE_VIOLAZIONE_ENTITA=5
			ELSE IF(@QUOTA_ENTITA>10) SET @CLASSE_VIOLAZIONE_ENTITA=3
			ELSE SET @CLASSE_VIOLAZIONE_ENTITA=1
			-- GRAVITA SELEZIONATO (FINO A CHE NON IMPLEMENTIAMO IL CALCOLO)
			SELECT @CLASSE_VIOLAZIONE_GRAVITA=CLASSE_VIOLAZIONE FROM TIPO_SANZIONI_PARAMETRI WHERE CODICE=@GRAVITA
			
			SET @SOMMA_VIOLAZIONI=(@CLASSE_VIOLAZIONE_DURATA+@CLASSE_VIOLAZIONE_GRAVITA+@CLASSE_VIOLAZIONE_ENTITA)/3
			IF(@SOMMA_VIOLAZIONI<3) SET @QUOTA_RIDUZIONE=10
			ELSE IF(@SOMMA_VIOLAZIONI<4) SET @QUOTA_RIDUZIONE=30
			ELSE SET @QUOTA_RIDUZIONE=50	
			
			DECLARE @AMMONTARE DECIMAL(10,2)
			IF(@PAGAMENTO_AMMESSO>0) SET @AMMONTARE=ROUND(@QUOTA_RIDUZIONE*@PAGAMENTO_AMMESSO/100,2)
			ELSE SET @AMMONTARE=0
			
			UPDATE SANZIONI SET DATA_APPLICAZIONE=GETDATE(),AMMONTARE=@AMMONTARE,DURATA=@DURATA,GRAVITA=@GRAVITA,
			VALORE_ENTITA=@ENTITA WHERE ID_SANZIONE=@ID_SANZIONE
			SELECT * FROM VSANZIONI WHERE ID_SANZIONE=@ID_SANZIONE
		END
	END
END
