ALTER PROCEDURE SpPsrRevisioneRiepilogoRup
(
	@ID_UTENTE INT,
	@ID_BANDO INT = NULL
)
AS
	--DECLARE @ID_BANDO INT=755
	
	DECLARE @NR_PAG INT, @NR_PAG_ISTRUITE INT, @NR_PAG_NEI_LOTTI INT, @NR_PAG_VALIDATE INT;

	SELECT 
		@NR_PAG = COUNT(*),
		@NR_PAG_ISTRUITE = SUM(CASE 
								WHEN C.SEGNATURA_REVISIONE IS NOT NULL 
									THEN 1 
								ELSE 0 
								END),
		@NR_PAG_NEI_LOTTI = SUM(CASE 
								WHEN C.ID_LOTTO IS NOT NULL 
									THEN 1 
								ELSE 0 
								END),
		@NR_PAG_VALIDATE = SUM(CASE 
								WHEN (L.REVISIONE_CONCLUSA = 1 
										OR (L.NUMERO_ESTRAZIONI > 0 AND C.SELEZIONATA_X_REVISIONE = 0)
										OR (C.SEGNATURA_REVISIONE IS NOT NULL AND C.APPROVATA = 1)) 
									THEN 1 
								ELSE 0 
								END)
	FROM CTE_TESTATA_VALIDAZIONE C
		LEFT JOIN LOTTO_DI_REVISIONE L ON C.ID_LOTTO = L.ID_LOTTO
	WHERE 1 = 1
		AND ((@ID_BANDO IS NULL 
				AND C.ID_BANDO IN (SELECT DISTINCT ID_BANDO 
									FROM BANDO_RESPONSABILI 
									WHERE ID_UTENTE = @ID_UTENTE 
									AND ATTIVO = 1)) 
			OR C.ID_BANDO = @ID_BANDO)  


	DECLARE @NR_LOTTI INT, @NR_LOTTI_CAMPIONATI INT, @NR_LOTTI_CONCLUSI INT

	SELECT 
		@NR_LOTTI = COUNT(*),
		@NR_LOTTI_CAMPIONATI = SUM(NUMERO_ESTRAZIONI),
		@NR_LOTTI_CONCLUSI = SUM(CASE 
									WHEN REVISIONE_CONCLUSA = 1 
										THEN 1 
									ELSE 0 
								END)
	FROM LOTTO_DI_REVISIONE 
	WHERE 1 = 1
		AND ((@ID_BANDO IS NULL 
				AND ID_BANDO IN (SELECT DISTINCT ID_BANDO 
								FROM BANDO_RESPONSABILI 
								WHERE ID_UTENTE = @ID_UTENTE 
									AND ATTIVO = 1) ) 
			OR ID_BANDO = @ID_BANDO )

	SELECT 
		@NR_PAG AS NR_PAG_PERVENUTE,
		ISNULL(@NR_PAG_ISTRUITE, 0) AS NR_PAG_ISTRUITE,
		ISNULL(@NR_PAG_NEI_LOTTI, 0) AS NR_PAG_NEI_LOTTI,
		ISNULL(@NR_PAG_VALIDATE, 0) AS NR_PAG_VALIDATE,
		@NR_LOTTI AS NR_LOTTI,
		ISNULL(@NR_LOTTI_CAMPIONATI, 0) AS NR_LOTTI_CAMPIONATI,
		ISNULL(@NR_LOTTI_CONCLUSI, 0) AS NR_LOTTI_CONCLUSI
GO